	TITLE CAFDTR Cafard DTR control
	SUBTTL Written by Mark Crispin/MRC

; Copyright (C) 1985 Mark Crispin.  All rights reserved.

	SEARCH MACSYM,MONSYM	; system definitions
	SALL			; suppress macro expansions
	.DIRECTIVE FLBLST	; sane listings for ASCIZ, etc.

;  The following "site switches" are unsupported, and subject to removal in
; a future version of this module.  They exist only because TOPS-20 doesn't
; have a DEC-defined DTR control functionality.

IFNDEF FTCSSE,<FTCSSE==:0>	; DEC CSSE DTRFE.MIC and HANGUP.MIC files
IFNDEF FTSTAN,<FTSTAN==:0>	; Stanford DTR control

A=1				; JSYS, temporary AC's
B=2
C=3
D=4

	.PSECT CODE

; $DTRON - Assert DTR
; Accepts:
;	A/ designator of line to assert DTR on
;	CALL $DTRON
; Returns +1: Always

$DTRON::SAVEAC <A,B,C>
IFN FTCSSE,<
	MOVX B,.MORSP		; get existing speed
	MTOPR%
	IFNJE.
	  HRRZ B,.MOSPD		; setting speed on remote line asserts DTR
	  MTOPR%
	   ERJMP .+1
	ENDIF.
>;IFN FTCSSE
IFN FTSTAN,<
	MOVX B,.MODUP		; turn DTR on
	MTOPR%
	 ERJMP .+1
>;IFN FTSTAN
	RET

; $DTROF - Drop DTR
; Accepts:
;	A/ designator of line to drop DTR on
;	CALL $DTROF
; Returns +1: Always

$DTROF::SAVEAC <A,B>
IFN FTCSSE,<
	MOVEI B,-2		; drop DTR function
	MTOPR%
	 ERJMP .+1
>;IFN FTCSSE
IFN FTSTAN,<
	MOVX B,.MOHUP		; turn DTR off
	MTOPR%
	 ERJMP .+1
>;IFN FTSTAN
	RET

	XLIST
	LIT
	LIST

	END
