;-*-Midas-*-

	Title

.DECSAV

A=5
B=6
C=7
T=12
TT=13
P=17

PDLen==40
BufSiz==1000

.INSRT MID:SYMBOLS
.INSRT MID:MACROS

PDL:	-PDLen,,.
	Block PDLen

Line:	0
LinBuf:	Block BufSiz

Begin:	RESET%
	Move P,PDL

GetLin:	Movei 1,.PRIIN
	Hrroi 2,LinBuf
	Movei 3,BufSiz*5-1
	Movei 4,^J
	SIN%			;Suck in a line
	  Erjmp Done
	Setz 1,
	Idpb 1,2		;Make sure ASCIZ
	Aos Line
	Cail 3,BufSiz*5-1
	  Jrst Done

	Move 2,[440700,,LinBuf]
ChkLin:	Ildb 1,2
	Jumpe 1,GetLin
	Cail 1,40
	  Jrst ChkLin
	Caie 1,^M
	  Cain 1,^J
	    Jrst ChkLin
	Caie 1,^L
	  Cain 1,^I
	    Jrst ChkLin
	Type "Line "
	Movei 1,.PRIOU
	Move 2,Line
	Movei 3,10.
	NOUT%
	  Nop
	Type ": "
	Move 3,[440700,,LinBuf]
OutLin:	Ildb 1,3
	Jumpe 1,GetLin
	Caie 1,^M
	  Cain 1,^J
	    Jrst OutLi0
	Caie 1,^L
	  Cain 1,^I
	    Jrst OutLi0
	Caige 1,40
	  Jrst [Movei 2,100(1)
		CType "^
		Movei 1,(2)
		Jrst OutLi0]
OutLi0:	PBOUT%
	Jrst OutLin

Done:	HALTF%
	Jrst Done

	End Begin
