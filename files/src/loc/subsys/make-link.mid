;-*-Midas-*-

	Title @MAKE-LINK - Make a custom .EXE bootstrapper

.DECSAV

 A=5
 B=6
 C=7
 D=10
 T=11
TT=12
 P=17

PDLen==20
JCLen==40

.INSRT SYSTEM:SYMBOLS
.INSRT SYSTEM:MACROS

PDList:	-PDLen,,.
	Block PDLen

JCLbuf:	Block JCLen

UserNo:	0
UNamLn:	0
UserNm:	Block 20

Vecp:	0
EndIns:	0

FNamLn:	0

IJFN:	0
OJFN:	0

GJ1blk:	GJ%FOU+.GJDEF
	.NULIO,,.NULIO
	Block 3
	-1,,[Asciz "EXE"]
	Block 3

GJ2blk:	GJ%OFG\GJ%XTN+.GJDEF
	.NULIO,,.NULIO
	-1,,[Asciz "SYS"]	;device
	Block 2
	-1,,[Asciz "EXE"]	;extension
	Block 3
	G1%SLN+0		;don't expand logical names

GJ3blk:	GJ%OLD+.GJDEF
	.NULIO,,.NULIO
	-1,,[Asciz "SYS"]
	Block 2
	-1,,[Asciz "EXE"]
	Block 3

.INSRT SYSTEM:PRARG

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;	Start of bootstrap code
;;
boot1b==.
Evec0l==10.
Evec0:	Repeat Evec0l,JSR PEvec

PEvec:	0
	Hrrz 17,PEvec
	Subi 17,Evec0+1
	Jrst Start
boot1l==.-boot1b

boot2b==.
Evec1l==1
Evec1:	Jrst Enter

Enter:	Movei 17,0		;Filled in later
boot2l==.-boot2b

boot3b==.
Start:	Move 0,1		;Save ac1
	Move 16,2		;and ac2
	Movx 1,GJ%OLD\GJ%SHT
	Hrroi 2,File
	GTJFN
	  Jrst [Type "?Link to non-existent file -- "
		AType File
		HALTF
		Jrst Start]
	Hrl 17,1		;Save JFN in LH of AC17, evec is in RH
	Move 1,[PurCod,,4]
	BLT 1,15		;BLT bootstrapping code into ACs
	Seto 1,			;Setup
	Movsi 2,.FHSLF		;for
	Setz 3,			;PMAP
	Jrst 4			;then run.

PurCod:	PMAP			;4
	Hlrz 1,17		;5 (jfn)
	Hrli 1,.FHSLF		;6
	GET			;7
	Movei 1,.FHSLF		;10
	GEVEC			;11
	Add 17,2		;12
	Move 1,0		;13 restore saved AC1
	Move 2,16		;14 restore saved AC2
	Jrst (17)		;15
;	saved AC2		;16
;	jfn,,evec		;17

File:	Block 40./5

constants

boot3l==.-boot3b
;;
;;	End of bootstrap code
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Begin:	RESET
	Move P,PDList
	GJINF
	Movem 1,UserNo
	Move 2,1
	Hrroi 1,UserNm
	DIRST
	  .Lose
	Movei T,1
	Move TT,UserNm-1(T)
	Trne TT,376
	  Aoja T,.-2
	Movem T,UNamLn
	Movei 1,.RSINI
	RSCAN
	  .Lose
	Movei 1,.PRIIN
	Hrroi 2,JCLbuf
	Movei 3,JCLen*5-1
	Movei 4,^J
	SIN
	Move 2,[440700,,JCLbuf]
Loop:	Ildb 1,2
	Cain 1,^J
	  Jrst [Type "Usage is
@MAKE-LINK new-name.EXE old-name.EXE [fixed entry vector]"
		Jrst Die]
	Caie 1,40
	  Jrst Loop

GetEm:	Movei 1,GJ1blk
	GTJFN
	  .Lose
	Movem 1,OJFN
	Movei 1,GJ2blk
	Push P,2
	GTJFN
	  .Lose
	Movem 1,IJFN
	Movei 1,GJ3blk
	Pop P,2
	GTJFN			;Now make sure it really exists...
	  .Lose
	RLJFN
	  Nop
	Move 1,2
	Movei 3,10.
	NIN			;See if there's an entry-vector specified.
	  Jrst GotEm
	Setom Vecp		;Yep.
	Hrrm 2,Enter

GotEm:	Hrroi 1,File
	Move 2,IJFN
	Move 3,[111100,,JS%PAF]
	JFNS
	  .Lose

	Movei T,1
	Move TT,File-1(T)
	Trne TT,376
	  Aoja T,.-2		;T/ # words in object filename.
	Movem T,FNamLn

	Move 1,OJFN
	Move 2,[Field(36.,OF%BSZ)+OF%WR]
	OPENF
	  .Lose

Create:	Move 1,OJFN
	Move 2,[-boot1l,,boot1b-1]
	Skipe Vecp
	  Move 2,[-boot2l,,boot2b-1]
	BOUT
	Move 3,2
	Move 2,1(3)
	BOUT
	Aobjn 3,.-2
	Move 2,[-boot3l,,boot3b-1]
	BOUT
	Move 3,2
	Move 2,1(3)
	BOUT
	Aobjn 3,.-2
	Move 2,[Evec0l,,Evec0]
	Skipe Vecp
	  Move 2,[Evec1l,,Evec1]
	Movem 2,EndIns
	BOUT
	Setz 2,
	BOUT

	Movni 2,7
	Sub 2,UNamLn
	Sub 2,FNamLn
	Movss 2
	Hrri 2,3		;-#words+2,,3
	BOUT
	Sub 2,[-1,,1]		;-#words+1,,2
	BOUT

	Movei 2,6
	BOUT
	Move 2,[.OSMIDAS]
	BOUT
	Move 2,[Sixbit "MAKLNK"]
	BOUT
	GTAD
	Move 2,1
	Move 1,OJFN
	BOUT
	Movei 2,6
	BOUT
	Add 2,UNamLn
	BOUT
	Movs 3,UNamLn
	Movns 3
	Move 2,UserNm(3)
	BOUT
	Aobjn 3,.-2
	Movs 3,FNamLn
	Movns 3
	Move 2,File(3)
	BOUT
	Aobjn 3,.-2
	Move 2,EndIns
	BOUT
	CLOSF
	  Nop
Die:	.Logout
	Jrst Die

	End Begin
