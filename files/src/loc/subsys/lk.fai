;SX:<B.BOMBADIL>LK.FAI.2,  6-Apr-80 23:17:33, Edit by B.BOMBADIL
;remove references to Andrew's macro library

Title LK - detect all terminal links
Subttl Andrew Knutsen, April 1978

Search Monsym, Macsym
.require sys:macrel
Asuppress
Xall
Nolit

start:	reset
	move 17,[iowd pdlen, pdlist]
	Hrroi 1,[Asciz "CTY:"]
	STDEV
	  Tdza 2,2
	    Tlz 2,-1
	Movem 2,CTY		;CTY's TTY#
	movsi 11,-165
loop:	hrrz 2,11
	tro 2,400000
	hrloi 1,(tl%who)
	tlink
	 jrst terr
	move 6,2
loop1:	jumpl 6,eloop1
	setz 5,
	lshc 5,11
	movei 12,[asciz/ is peeking at /]
	move 2,5
	tro 2,400000
	hrloi 1,(tl%who)
	tlink
	 jrst terr
	move 3,2
	hrrz 1,11
loop2:	setz 2,
	lshc 2,11
	camn 1,2
	 jrst double
	jumpn 3,loop2
prntit:	movei 1,400000(5)
	call doinf
	hrro 1,12
	psout
	movei 1,400000(11)
	call doinf
	hrroi 1,[byte(7) 15,12,0]
	psout
	caia
eloop1:	 lsh 6,11
eloop:	jumpn 6,loop1
terr:	aobjn 11,loop
	haltf
	jrst start

double:	caml 1,5
	 jrst eloop
	movei 12,[asciz/ is talking to /]
	jrst prntit

doinf:	hrroi 2,4
	movei 3,2
	setz 4,
	getji
	 ercal warn
	movei 3,-400000(1)
	jumpe 4,nobody
	movei 1,.priou
	move 2,4
	dirst
	 ercal warn
	jrst shotty

nobody:	camn 3,CTY
	  jrst [hrroi 1,[asciz "the CTY"]
		psout
		ret]
	hrroi 1,[asciz /an empty terminal/]
	psout
shotty:	camn 3,CTY
	  jrst [hrroi 1,[asciz / (on the CTY)/]
		psout
		ret]
	hrroi 1,[asciz/ (tty/]
	psout
	movei 1,.priou
	move 2,3
	movei 3,10
	nout
	 ercal warn
	movei 1,")"
	pbout
	ret

;here on a jsys error

warn:	hrroi 1,[asciz/JSYS Error - /]
	esout
	movei 1,.priou
	hrloi 2,.fhslf
	setz 3,
	erstr
	 jfcl
	 jfcl
	ret

;impure storage

CTY:	Block 1
ttsym:	block 1
systad:	block 1
pdlen==10
pdlist:	block pdlen

	end	start
