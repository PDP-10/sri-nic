	Title  LastDirUpdate

.DECSAV

A=1
B=2
C=3
D=4
Q=12
P=17

.INSRT	MID:MACROS
.INSRT	MID:SYMBOLS

Define RetSkp
	Jrst Popj1
Termin

PDLen==40
PDL:	Block PDLen


Start:	RESET%
	Move	P,[PDL(-PDLen)]	;init stack
	Movei	1,.FHSLF
	Seto	3,
	EPCAP%

	TypeCR	"Directory	Last Modified"
	Movx	1,GJ%SHT\GJ%IFG\GJ%FLG
	Hrroi	2,[Asciz /PS:<*>*.DIRECTORY/]
	GTJFN%
	 Erjmp	Error
	Movem	1,Q
	
Loop:	Movei	1,.PRIOU
	Hrrz	2,Q
	Setz	3,
	JFNS%
	 Erjmp	Error
	Movei	1,.CHTAB
	PBOUT%
	Hrrz	1,Q
	Move	2,[1,,.FBADR]
	Movei	3,FDBBLK
	GTFDB%
	 Erjmp	Error
	Move	1,FDBBLK	;this disk addr (directory index block)
	And	1,[000177,,777777]
	Add	1,[400000,,0]	;from ps:
	Movei	2,1000		;read one page
	Movei	3,DSKBLK	;into memory
	Setz    4,
	DSKOP%
	 Erjmp	Error
	Jumpn	1,[Push P,1
		   Type "DSKOP% error "
		   Jrst Die]
	Move	1,DSKBLK	;this disk addr
	And	1,[000177,,777777]
	Add	1,[400000,,0]	;from ps:
	Movei	2,1000		;read one page
	Movei	3,DSKBLK	;into memory
	Setz    4,
	DSKOP%
	 Erjmp	Error
	Jumpn	1,[Push P,1
		   Type "DSKOP% error "
		   Jrst Die]
	Movei	1,.PRIOU
	Move	2,DSKBLK+24	;last update tad of dir
	Setz	3,
	ODTIM%
	 Erjmp	.+1
	TypeCR	""
	Move	1,Q
	GNJFN%
	 Erjmp	Done
	Jrst	Loop

Popj1:	Aos	(P)
R:	Ret

Error:	Call	ErrRet
	Jrst	Die

ErrRet:	Movei	1,.PRIOU
	Hrloi	2,.FHSLF
	Setz	3,
	ERSTR%
	 jfcl
	 jfcl
	Ret
Done:	TypeCR	"done."
	Jrst	Die
Die:	Haltf%
	Jrst	Die

FDBBLK=100000
DSKBLK=200000

	End Start
