	Title PFIND

.DECSAV

SBP==13
BP==14
P=17

PDLen==20

.INSRT LIB:SYMBOLS
.INSRT LIB:MACROS
.INSRT LIB:STCMP

HlpMsg:	Asciz "Usage: pfind string"

filnam:	asciz /PS:<OPERATOR>DIR.BAKE/
filjfn:	0
pagcnt:	0
bol:	0
found:	0

string:	block 40

$CRLF:	Asciz /
/

PDList:	-PDLen,,.
	Block PDLen

Begin:	RESET
	Move P,PDList

	Movei 1,.RSINI
	RSCAN
	 erjmp rnderr

sChaff:	PBIN
	Cain 1,.chlfd
	  Jrst Help
	Caie 1,.chspc
	  Jrst sChaff
	move bp,[440700,,string]
schaf3:	PBIN%
	Caie 1,.chcrt
	 Cain 1,.chlfd
	  setz 1,
	Idpb 1,BP
	Jumpn 1,schaf3

	Movx 1,GJ%OLD\GJ%SHT
	Hrroi 2,filnam
	GTJFN%
	 Erjmp NoFil
	Movem 1,filjfn
	Move 2,[070000,,OF%RD]
	OPENF%
	 erjmp nofil
	SIZEF%
	 erjmp rnderr
	movem 3,pagcnt
	cail 3,500
	 jrst [typecr "File too large."
	       jrst die]

	hrlz 1,filjfn
	move 2,[.fhslf,,buffer]
	movx 3,pm%cnt!pm%rd
	hrr 3,pagcnt
	PMAP%
	 erjmp rnderr

	movei 1,buffer
	add 1,pagcnt
	imuli 1,1000
	setzm (1)

;uppercase search string
	move sbp,[440700,,string]
upsstr:	ildb 1,sbp
	cail 1,"a
	 caile 1,"z
	  trna
	   subi 1,40
	dpb 1,sbp
	jumpn 1,upsstr

	move bp,[440700,,<buffer*1000>]
	movem bp,bol
miss:	move sbp,[440700,,string]
	ildb 2,sbp
nxtchr:	ildb 1,bp
	jumpe 1,eof
	caie 1,.chcrt
	 cain 1,.chlfd
	  jrst beglin
	cail 1,"a
	 caile 1,"z
	  trna
	   subi 1,40
	came 1,2		;first char match?
	 jrst nxtchr
	move 1,[440700,,string]
	ibp 1
	move 2,bp
	$STCMP
	txne 1,sc%sub
	 jrst cpy
	jrst miss

beglin:	movem bp,bol
	jrst miss

cpy:	movei 1,.priou
	move 2,bol
	movei 3,^d256
	movei 4,.chlfd
	SOUT%

	ildb 1,bp
	caie 1,.chlfd
	 jrst .-2

	aos found

	jrst beglin

eof:	move 2,found
	caige 2,5
	 jrst nocount
	movei 1,.priou
	movei 3,10.
	NOUT%
	 jfcl
	TypeCR " matches found."

nocount:
	seto 1,
	move 2,[.fhslf,,buffer]
	movx 3,pm%cnt
	hrr 3,pagcnt
	PMAP%
	 erjmp rnderr

	move 1,filjfn
	CLOSF%
	 erjmp .+1
	Jrst Die

NoFil:	Call PErr
rnderr:	Call PErr

PErr:	Push P,1
	Type "?"
	Movei 1,.PRIOU
	Pop P,2
	Movei 3,8.
	NOUT%			;print error number
	 jfcl
	Type " - "
	Movei 1,.PRIOU
	Move 2,[.fhslf,,-1]
	Setz 3,
	ERSTR%
	 jfcl
	 jfcl
	Jrst Die

Help:	AType HlpMsg
Die:	haltf%
	Jrst Die

Buffer==<.+1000>_-9

	End Begin
