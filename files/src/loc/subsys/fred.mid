;-*-Midas-*-

	Title @FREED - Show Free Disk

.DECSAV

 A=5
 B=6
 C=7
 D=10
 E=11
 T=12
TT=13
 P=17

PDLen==20

.INSRT MID:SYMBOLS
.INSRT MID:MACROS
.INSRT MID:PRARG

PDList:	-PDLen,,.
	Block PDLen

Strnam:	Block 10

Begin:	RESET
	Move P,PDList
	Type "  Name Us%ed Free
"
	MOVE 1,[SIXBIT /DEVNAM/]
	SYSGT
	HRRZ D,2		;Table#
	Move 1,[Sixbit "DEVCHR"]
	SYSGT
	Hrrz E,2
	Hllz C,2

GSTRNM:	HRLZ 1,C
	HRR 1,E			;Offset,,table#
	GETAB
	  Erjmp GNSTRN
	Ldb T,[.BP DV%TYP,1]
	Caie T,.DVDSK
	  Jrst GNSTRN
	Hrlz 1,C
	Hrr 1,D
	GETAB
	  Erjmp GNSTRN
	Jumpe 1,Done
	HLRZ 2,1		;If no more devices, it's SIXBIT/STRnnn/
	CAIN 2,(SIXBIT /STR/)
	  Jrst Done
	Camn 1,[Sixbit "DSK"]
	  Jrst GNSTRN
	MOVE 3,[440700,,STRNAM]
	Seto 4,
ST6TO7:	SETZ 2,			;Turn sixbit structure name into asciz
	ROTC 1,6		;string.
	ADDI 2,40
	IDPB 2,3
	Aoj 4,
	JUMPN 1,ST6TO7
	SETZ 2,
	IDPB 2,3		;Null terminate.

	Movei 1,40
Karf:	Cail 4,5
	  Jrst Karfle
	PBOUT
	Aoja 4,Karf

Karfle:	HRROI 1,STRNAM
	STDEV
	  Erjmp GNSTRN
	MOVE 1,2		;STDEV returns device specifier in AC2
	GDSKC
	Move T,1
	Move TT,1
	Add TT,2		;T/ used, TT/ total size
	Push P,2
	FLTR T,T
	FLTR TT,TT
	FDVR T,TT
	FMPR T,[100.0]
	AType Strnam
	CType 40
	Movei 1,.PRIOU
	Move 2,T
	Move 3,[FL%ONE+Field(2,FL%FST)+Field(2,FL%SND)]
	FLOUT
	  Nop
	CType 40
	Movei 1,.PRIOU
	Pop P,2
	MOVEI 3,10.
	NOUT
	  Nop
	Type "
"

GNSTRN:	Aobjn C,GSTRNM		;Step to next structure.
Done:	.Logout

	End Begin