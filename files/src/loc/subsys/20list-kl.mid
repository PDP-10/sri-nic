;-*-Midas-*-

	Title @20LIST - Generate a list of 20 turkeys from PFIND data

.DECSAV

A=5
B=6
C=7
T=12
TT=13
L1=14
L2=15
P=17

PDLen==40

GrpSiz==20.			;# people in each group
nGroup==4.			;# of groups to generate
MaxLin==10000.			;Maximum # of lines in input file we can hack


.INSRT MID:SYMBOLS
.INSRT MID:MACROS

PDL:	-PDLen,,.
	Block PDLen

seed:	2271402.
const1: 3017.
const2:	111325.
const3: 7741.
fcons3:	7741.0

RanArg:	0
JFN:	0

File:	Asciz "KL:<PFIND>DIR.BAKE"	;PFIND data file

CRLF:	Asciz "
"

nLines:	0			;# of person-line in input file
LineBP:	Block MaxLin

nChose:	0
Chosen:	Block nGroup*GrpSiz

.INSRT MID:PRARG

Begin:	RESET
	Move P,PDL
	Movx 1,GJ%SHT\GJ%OLD
	Hrroi 2,File
	GTJFN
	  Jrst [Type "?Can't get source file "
		AType File
		Type " -- "
		Call Barf
		Jrst Die]
	Movem 1,JFN
	Move 2,[Field(36.,OF%BSZ)+OF%RD]
	OPENF
	  .Lose
	SIZEF
	  .Lose
	Movss 1			;JFN,,0
	Move 2,[.FHSLF,,Mapage]
	Txo 3,PM%CNT\PM%RD
	PMAP
	  .Lose
	Move A,[440700,,Mapadr]
	Jrst SaveEm

Loop:	Ildb T,A
	Jumpe T,GotEm
	Caie T,^J
	  Jrst Loop
SaveEm:	Aos T,nLines
	Movem A,LineBP-1(T)
	Jrst Loop

GotEm:	Movei 1,.PRIOU
	Move 2,nLines
	Movei 3,10.
	NOUT
	  Nop
	Type " person-lines in source file "
	Movei 1,.PRIOU
	Move 2,JFN
	Movx 3,JS%SPC
	JFNS
	AType CRLF
	TIME
	Add 1,Seed
	Movem 1,Seed
	Movei L1,nGroup

GLoop:	Type "
----------------------------------------------------------------------

"
	Movei L2,GrpSiz
GLoop0:	Move T,nLines
	Subi T,1
	Call Random		;Get a random # from 0-T
	Movei 1,.PRIOU
	Move 2,LineBP(T)
	Movei 3,HInfinity
	Movei 4,^J
	SOUT
	Sojg L2,GLoop0
	Sojg L1,GLoop

Die:	.Logout
	Jrst Die

Random:	Movem T,RanArg
Rand0:	Call RanGen
	Movs TT,nChose
	Movns TT
Rando0:	Camn T,Chosen(TT)
	  Jrst [Move T,RanArg
		Jrst Rand0]
	Aobjn TT,Rando0
	Aos TT,nChosen
	Movem T,Chosen-1(TT)
	Return

rangen:	move a,seed
	IMul a,const2
	Add a,const1
	IDiv a,const3
	movem a,seed
	FLTR b,b
	FDVR b,fcons3
	FLTR c,T
	fmp b,c
	fix t,b
	return

Variables
Constants

Mapage==<./777>+1
Mapadr=Mapage*1000

	End Begin
