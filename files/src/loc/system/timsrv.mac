
	TITLE TIMSRV Serve up the time per RFC 738, IEN 142
	SUBTTL /Rcc
;
;Written October/November 1981 by Robert C Clements, at
; Bolt Beranek and Newman, Inc.
;
; Copyright (c) 1981 Bolt Beranek and Newman, Inc., Cambridge MA 02238
;
; This software may be copied freely for use within the Internet
; community, but may not be sold or used for profit without permission
; of the author and copyright holder.
;

	SEARCH MACSYM,MONSYM
	.REQUIRE SYS:MACREL

F=0
A=1
B=2
C=3
D=4
E=5

Q1=6
Q2=7
Q3=10

X=11
Y=12

P=17

OPDEF CALL [PUSHJ P,]
OPDEF RET [POPJ P,]
DEFINE MSG(MESS)<
	HRROI B,[ASCIZ \MESS\]
	CALL DOSOUT
>

PDLL==100			;STACK LENGTH

;FLAGS IN AC F

L.ERR1==1			;COMMENT FIRST TIME IN WAIT LOOP FOR IQ
L.LAST==2			;LAST TIME THRU IN NCP SCAN

;ASCII CHARS BY NAME

CH.TAB==11			;HORIZ TAB
CH.LF==12			;LINEFEED
CH.CR==15			;CARR RET
CH.SPC==40			;SPACE

; MISC PARAMS

UDPPRN==^D17			;USER DATAGRAM PROTOCOL NUMBER
TSPORT==^D37			;TIME SERVER PORT NUMBER
AQ%SPT==1B1			;NOT IN MONSYM?
RCVLEN==100			;WORDS OF RCV BUFFER
NSTATS==20			;WORDS OF TCB STATUS TO READ

; LAYOUT OF INET BUFFER FOR UDP PROTOCOL


.IPKVR==0			;VERSION, TYPE OF SERVICE
.IPKSG==1			;SEGMENTATION INFO
.IPKPR==2			;PROTOCOL, TIME TO LIVE, ...
.IPKSH==3			;SOURCE HOST
.IPKDH==4			;DEST HOST
.UDPPO==5			;PORT NUMBERS
.UDPLN==6			;LENGTH, CHECKSUM
.TSVCK==7			;TIME SERVER CLOCK
TXLEN==.TSVCK+1+1		;ONE FOR HEADER

;TCP JSYS DEFINITIONS

OPDEF TCPOP [JSYS 742]		;OPEN
OPDEF TCPCL [JSYS 743]		;CLOSE
OPDEF TCPSN [JSYS 740]		;SEND
OPDEF TCPRC [JSYS 741]		;RECEIVE
OPDEF TCPST [JSYS 745]		;STATUS
OPDEF TCPCH [JSYS 746]		;CHANNEL
OPDEF TCPAB [JSYS 747]		;ABORT
OPDEF TCPSS [JSYS 744]		;SET SECURITY

TCP%JS==1B0			;JCN SUPPLIED
TCP%WT==1B1			;WAIT
TCP%FS==1B5			;FORCE SYNC
TCP%PS==1B6			;PERSIST ON OPEN
TCP%ST==1B7			;RETURN STATISTICS
TCP%SC==1B8			;SECURE CONNECTION
TCP%HP==1B9			;HIGH PRIORITY
TCP%VT==1B10			;VIRTUAL TERMINAL

;START OF PROGRAM

GO:	RESET			;CLEAR THE WORLD
	MOVE P,PDP		;SET UP A STACK
	MOVEI F,0		;FLAGS ALL CLEAR TO START
	SETOM OJFN		;NO OUTPUT JFN YET
	SETO A,0		;RELEASE ALL INTERNET QUEUES
	RELIQ%			; ..
	 JFCL
	MOVEI A,.FHSLF
	RPCAP
	SETO C,0		;ASK FOR CAPS
	EPCAP			; ..
GTWTL:	GTAD			;TIME KNOWN YET?
	JUMPL A,[MOVEI A,^D5000
		DISMS
		JRST GTWTL]	;WAIT UNTIL TIME SET
	CALL SETFRK		;SET UP AND START INFERIORS
	MOVEI A,-4		;WAIT FOR ANY INFERIOR TO CRASH
	WFORK
	JFCL
WFORKX:	RESET			;LET GO OF STUFF
	SETO A,0		;LET GO OF ANY INET Q'S
	RELIQ%
	 JFCL
	SKIPN DEBUG		;DEBUGGING?
	JRST WAITGO		;NO
	MSG <
TIMSRV restarting
>
WAITGO:	MOVEI A,^D120000
	SKIPE DEBUG
	MOVEI A,^D5000
	DISMS
	JRST GO

SETFRK:	MOVSI X,-NFORKS
STFRKL:	HRRZ A,FRKTBL(X)	;START A FORK AT THIS PC
	TXO A,CR%MAP!CR%CAP!CR%ST ; WITH MY MAP AND CAPS
	MOVEI B,0		;NO AC SETUP BLOCK
	CFORK			;LET IT RIP
	 JRST CFORKX		;OOPS, COULDN'T DO IT
	HRRZM A,FORKHT(X)	;REMEMBER FORK HANDLE
	AOBJN X,STFRKL		;LOOP OVER ALL PROCESSES
	RET			;DONE

CFORKX:	MSG <TIMSRV - Can't create forks
>
	JRST WAITGO
FRKTBL:	EXP TCPFRK,UDPFRK
;;;	EXP NCPFRK		;NOT DONE YET
 NFORKS==.-FRKTBL

;HERE IS THE TCP FORK

TCPFRK:	MOVEI F,0		;FORK FLAGS OFF
TCPTOP:	RESET			;CLEAR EVERYTHING
	MOVE P,[XWD -PDLL,TCPPDL]
	MOVEI A,TCB		;POINT AT LISTENING BLOCK
	MOVEI B,0		;NO PERSISTENCE
	MOVE C,[BYTE (9)3,2,0,0] ;BACKOFF PARAMS
	TXO A,TCP%WT
	TCPOP			;OPEN TCP CONNECTION
	 JRST TCPOPF		;FAILED
	MOVEM A,TCPJCN		;SAVE CONNECTION NUMBER
	HRLI A,(TCP%JS)		;ONLY THIS FLAG
	MOVSI B,-NSTATS		;WANT SOME STATISTICS
	MOVE C,[-NSTATS,,STATBF]
	TCPST
	 JRST TCPSTF		;STATUS FAILED??
	SKIPN DEBUG		;WANT TO SEE TALK?
	JRST TCPXN1		;NO
	MSG <TCP transaction from >
	MOVEI Q3,4		;FOUR BYTES OF INET ADDR
	MOVE Q2,[POINT 8,STATBF+7,3]
TCPLL1:	ILDB B,Q2
	CALL DECOUT
	SOJLE Q3,TCPLN1
	MSG <.>
TCPLN1:	JUMPG Q3,TCPLL1
	MSG < at >
	CALL DATOUT
	CALL CRLF
TCPXN1:	GTAD			;GET TIME AND DATE
	MOVE B,A		;INTO RIGHT AC
	CALL CVINET		;CONVERT TO NET FORMAT
	LSH B,4			;32 BITS IN LEFT OF WORD
	MOVEM B,TCPBUF		;PUT IT IN BUFFER
	MOVE A,[TCPPRO,,TCPRNG]
	BLT A,TCPRNE		;PUT RING POINTER UP FOR USE
	HRRZ A,TCPJCN		;GET THE JCN BACK
	TXO A,TCP%JS!TCP%WT	;JCN SUPPLIED, WAIT
	MOVEI B,TCPRNG		;POINT AT BUFFER RING
	MOVEI C,10		;TIMEOUT IN 8 SECONDS
	MOVE D,[BYTE (9)1,1,0,1] ;RE-XMT PARAMETERS
	TCPSN			;SEND BUFFER
	 JRST TCPSNF		;SEND FAILED
	HRRZ A,TCPJCN		;NOW CLOSE IT
	TXO A,TCP%JS!TCP%WT
	TCPCL			;CLOSE
	 JRST TCPCLF
	JRST TCPTOP		;READY FOR NEXT ONE

TCPSTF:	JFCL			;STATUS FAILED
TCPOPF:	JFCL			;OPEN FAILED
TCPSNF:	JFCL			;SEND FAILED
TCPCLF:	JFCL			;CLOSE FAILED
	RESET			;SOMETHING FAILED.
	MOVEI A,^D10000		;GO SLOW, TO AVOID
	DISMS			; EATING CPU ON PERM ERR
	JRST TCPTOP		;TRY AGAIN

TCB:	EXP TSPORT		;PORT NUMBER
	EXP 0,0			;FOREIGN HOST AND PORT ARE WILD
TCPPRO:	EXP 0
	EXP TCPBUF		;WHERE TO SEND FROM
	EXP 4			;LENGTH IN BYTES

NCPFRK:	MOVEI F,0		;NO FORK AC'S
	MOVE P,[XWD -PDLL,NCPPDL]
	WAIT
	JRST .-1		;;;NOT DONE YET

;MAIN LOOP. WAITS FOR A UDP REQUEST AND SERVICES IT

UDPFRK:	MOVEI F,0		;CLEAR FORK'S FLAGS
UDPTOP:	MOVE P,[XWD -PDLL,UDPPDL] ;OWN STACK
	CALL GETIQ		;GO GET THE INTERNET QUEUE
UDPTP1:	MOVEI A,RCVLEN		;LENGTH WE'LL ACCEPT
	MOVEM A,RCVHDR		; ..
	HRRZ A,IQH		;GET THE Q HANDLE
	MOVEI B,RCVHDR		;POINT AT BUFFER
	MOVEI C,0		;MBZ BITS
	RCVIN%			;WAIT FOR A DATAGRAM
	 JRST UDPSV1		;JUNK?
	SKIPN DEBUG		;WANT TO SEE TALK?
	JRST SRVXN1		;NO
	MSG <UDP transaction from >
	MOVEI Q3,4		;FOUR BYTES OF INET ADDR
	MOVE Q2,[POINT 8,RCVBUF+.IPKSH]
SRVL1:	ILDB B,Q2
	CALL DECOUT
	SOJLE Q3,SRVN1
	MSG <.>
SRVN1:	JUMPG Q3,SRVL1
	MSG < at >
	CALL DATOUT
	CALL CRLF
SRVXN1:	MOVE A,[SNDPRO,,SNDBUF]
	BLT A,SNDBUF+.UDPLN	;FILL IN PROTOTYPE OF MSG
	MOVE A,RCVBUF+.IPKSH	;SEND BACK TO THIS GUY
	MOVEM A,SNDBUF+.IPKDH	; ..
	LDB A,[POINT 16,RCVBUF+.UDPPO,15]	;HIS PORT
	DPB A,[POINT 16,SNDBUF+.UDPPO,31]
	GTAD			;GET THE CLOCK
	MOVE B,A		;INTO RIGHT AC FOR SUBR
	CALL CVINET		;CONVERT INTERNAL TO NET FORM
	LSH B,4			;OVER FOR NET BUFFER
	MOVEM B,SNDBUF+.TSVCK	;WHERE IT GOES IN THE ANSWER
	MOVEI A,TXLEN		;LENGTH OF TX BFR AND HDR
	MOVEM A,SNDHDR		; ..
	HRRZ A,IQH		;ON THIS Q
	MOVEI B,SNDHDR		;FROM THIS BUFFER
	MOVEI C,0		;MBZ BITS
	SNDIN%			;SEND IT
	 JRST UDPSV5		;FAILED
	JRST UDPTP1		;BACK TO TOP LOOP

UDPSV5:	SKIPA			;TX FAILURE
UDPSV1:	JFCL			;RX FAILURE
	SETO A,0		;RELEASE THE QUEUE
	RELIQ%			; ..
	 JFCL
	MOVEI A,^D5000		;DELAY, IN CASE OF WEIRD ERROR
	DISMS
	JRST UDPTOP		;TRY AGAIN

SNDPRO:	BYTE (8)105,0,0,40	;IP VER, LENGTHS
	EXP 0			;FRAGMENT INFO
	BYTE (8)100,UDPPRN,0,0	;TIME TO LIVE, PROTOCOL
	EXP .-.			;SRC HOST, BY MONITOR
	EXP .-.			;DEST HOST
	BYTE (16)TSPORT,.-.	;PORTS IN UDP
	BYTE (16)14,0		;LENGTH OF DATAGRAM

; THIS ROUTINE ASSIGNS THE INTERNET QUEUE FOR DATAGRAMS.
; IT WAITS AS NEEDED, IF THE Q IS BUSY

GETIQ:	TLZ F,L.ERR1		;NO ERR MSG YET
GETIQ1:	MOVE A,[QDBPRO,,QDBLK]	;COPY PROTOTYPE TO QDB
	BLT A,QDBLKE		; ..
	MOVEI A,QDBLK		;POINT AT QDB
	SETZB B,C		;MBZ AC'S
	ASNIQ%			;TRY FOR IT
	 JRST GETIQE		;FAILED
	MOVEM A,IQH		;SAVE HANDLE
	MOVEM B,SNDMAX		;SAVE MAX SNDIN'S
	RET

GETIQE:	TLOE F,L.ERR1		;ERROR MSG FIRST TIME ONLY
	JRST GETIQW		;WAIT FOR IT
	CAIE A,ASNSX2		;CONFLICT?
	JRST GETIQX		;NO, SOMETHING ELSE
	PUSH P,B		;SAVE OTHER JOB NUMBER
	MSG <% Internet Q busy, by job >
	POP P,B			;JOB NUMBER AGAIN
	CALL DECOUT
	CALL CRLF
GETIQW:	MOVEI A,^D5000		;WAIT BEFORE RETRYING
	DISMS
	JRST GETIQ1		;RETRY THE ASSIGN

GETIQX:	JSERR			;SOME OTHER ERROR
	CALL CRLF
	JRST GETIQW		;WAIT AND RETRY
;;;

DATOUT:	SKIPG A,OJFN
	MOVEI A,.PRIOU
	SETO B,0
	SETZ C,0
	ODTIM
	 ERJMP .+1
	RET

CRLF:	HRROI B,CRLFM		;TYPE A CRLF
DOSOUT:	SKIPG A,OJFN		;GET OUTPUT JFN, IF ANY
	MOVEI A,.PRIOU		;ELSE, PRIMARY
	MOVEI C,0		;ASCIZ CALLER STRING
	SOUT
	RET

CRLFM:	BYTE (7)CH.CR,CH.LF,0

DOBOUT:	SKIPG A,OJFN
	MOVEI A,.PRIOU
	BOUT
	RET

DECOUT:	SKIPG A,OJFN
	MOVEI A,.PRIOU
	MOVEI C,12
	NOUT
	 JFCL
	RET

TMBDIF==^D<365*41>+^D55		;1858 BASE VS 1900 BASE, IN DAYS

CVINET:	PUSH P,C		;PRESERVE A COUPLE REGS
	PUSH P,D		; ..
	MOVEI C,(B)		;TOPS20 FRACTION OF A DAY
	HLRZS B			;DAYS SINCE NOV 1858
	SUBI B,TMBDIF		;BRING DOWN TO 1900
	MULI C,^D<24*60*60>	;CONVERT TO SECONDS FROM 1/3 SEC
	DIV C,[1,,0]		; ..
	CAIL D,400000		;ROUND TO NEAREST SECOND
	ADDI C,1		;ROUND UP
	CAIL C,^D<24*60*60>	;WENT TO WHOLE DAY?
	JRST [	MOVEI C,0	;YES, COUNT A DAY
		AOJA B,.+1]
	IMULI B,^D<24*60*60>	;SECONDS FROM DAYS
	ADDI B,(C)		;SECONDS WITHIN TODAY
	POP P,D			;RESTORE REGS
	POP P,C			; ..
	RET			;RETURN RFC738 FORMAT TIME IN B

CVNETI:	PUSH P,C		;PRESERVE REGISTERS
	PUSH P,D		; ..
	IDIVI B,^D<24*60*60>	;GET DAYS AND SECONDS IN TODAY, GMT
	ADDI B,TMBDIF		;ADD OFFSET FOR MJD 0
	MUL C,[1,,0]		;CONVERT SECONDS TO FRACTION OF DAY
	DIVI C,^D<24*60*60>	; FOR TOPS20 FORMAT GTAD
	CAIL D,^D<24*60*60>/2	;ROUND IT
	ADDI C,1		; ..
	MOVSI B,(B)		;DAYS TO LH OF GTAD
	HRRI B,(C)		;FRACTION OF DAY TO RH
	POP P,D			;RESTORE REGS
	POP P,C			; ..
	RET			;GTAD FORMAT IN B

;CONSTANTS

QDBPRO:	BYTE (8)0,0,0,UDPPRN	;UDP PROTOCOL NUMBER
	EXP 0			;WILD FOREIGN HOST
	EXP 0			;WILD SOURCE HOST
	BYTE (16)TSPORT,0	;TIME SERVER PORT NUMBER
	BYTE (8)0,0,0,377	;MASK FOR PROTOCOL
	EXP 0			;MASK FOR FOREIGN HOST
	EXP 0			;MASK FOR SOURCE HOST
	BYTE (16)-1,0		;MASK FOR PORT NUMBER
NQDB==.-QDBPRO			;LENGTH OF ONE OF THESE

PDP:	XWD -PDLL,PDL		;INITIAL STACK POINTER
DEBUG:	EXP 0			;NON-ZERO FOR LOG/DEBUG MESSAGES

;VARIABLES/BUFFERS

FORKHT:	BLOCK NFORKS		;HANDLES OF INFERIORS

QDBLK:	BLOCK NQDB		;Q DESCRIPTOR BLK FOR ASNIQ
QDBLKE==.-1

OJFN:	BLOCK 1			;POSSIBLE OUTPUT JFN FOR LOGGING
IQH:	BLOCK 1			;INTERNET Q HANDLE
SNDMAX:	BLOCK 1			;NUMBER OF SNDIN'S ALLOWED
				; (WE'LL ONLY USE 1 AT A TIME)
RCVHDR:	BLOCK 1			;RECEIVE BUFFER HEADER
RCVBUF:	BLOCK RCVLEN+2		;LENGTH OF RCV BUFFER + TO BE SURE
SNDHDR:	BLOCK 1			;SEND BUFFER HEADER
SNDBUF:	BLOCK RCVLEN+2		;PLENTY OF SLOP
TCPJCN:	BLOCK 1			;JOB CONNECTION NUMBER
TCPRNG:	BLOCK 3			;RING HEADER
 TCPRNE==.-1
TCPBUF:	BLOCK 1			;ONLY ONE DATA WORD FOR TCP
STATBF:	BLOCK NSTATS		;SPACE FOR TCP TCB STATUS
PDL:	BLOCK PDLL+1		;STACK AREA
NCPPDL:	BLOCK PDLL+1		;STACK AREA
TCPPDL:	BLOCK PDLL+1		;STACK AREA
UDPPDL:	BLOCK PDLL+1		;STACK AREA

	END GO

