;-*-Midas-*-
; arguments: job# or -1, percentage or "SYSTEM"

	Title SJPRI -- Interface to SJPRI%

.DECSAV

 A=5
 B=6
 C=7
 T=10
TT=11
 N=14
BP=15
 L=16
 P=17

PDLen==20

.INSRT LIB:SYMBOLS
.INSRT LIB:MACROS

HlpMsg:	Asciz "Usage: SJPRI (job# | -1) (percentage | SYSTEM)"

JobNo:	0
PerNo:	0

$CRLF:	Asciz /
/
PDList:	-PDLen,,.
	Block PDLen

Begin:	RESET
	Move P,PDList

	Movei 1,.RSINI
	RSCAN%
	  Erjmp Die

sChaff:	PBIN%
	Cain 1,^J
	  Jrst Help
	Caie 1,40
	  Jrst sChaff
	Move 2,[440700,,Buffer]
GMore:	PBIN%
	Jumpe 1,EOL
	Caie 1,.CHCRT
	 Cain 1,.CHLFD
	  Jrst EOL
	Idpb 1,2
	Jrst GMore
EOL:	Setz 1,
	Idpb 1,2

	Move 1,[440700,,Buffer]
	Movei 3,10.
	NIN%
	 erjmp help
	Movem 2,JobNo

	Push P,1
	Movei 3,10.
	NIN%
	 erjmp TryAlp
	Hrlzm 2,PerNo
	Pop P,1
	Jrst SetIt	
TryAlp:	Pop P,1
	Ildb 2,1
	Caie 2,"S
	 Cain 2,"s
	  SkipA
	   Jrst Help
	Ildb 2,1
	Caie 2,"Y
	 Cain 2,"y
	  SkipA
	   Jrst Help
	Ildb 2,1
	Caie 2,"S
	 Cain 2,"s
	  SkipA
	   Jrst Help
	Move 1,[JP%SYS]
	Movem 1,PerNo

SetIt:	Move 1,JobNo
	Jumpge 1,SetPer
	Move 2,[-1,,4]
	Movei 3,.JIJNO
	GETJI%
	 erjmp error
	Move 1,4
SetPer:	Move 2,PerNo
	SJPRI%
	 erjmp error
	Jrst Die

Help:	AType HlpMsg
Die:	HALTF%
	Jrst Die
error:	movei 1,.priou
	move 2,[.fhslf,,-1]
	setz 3,
	ERSTR%
	 jfcl
 	 jfcl
 	 jfcl
	jrst die

Variables
Constants

Buffer:	0

	End Begin
