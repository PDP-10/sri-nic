;-*-Midas-*-

	Title @PALINDROMES - Find all single-word palindromes in W7

.DECSAV

A=5
B=6
C=7
Z=10
P=17

PDLen==40

.INSRT MID:SYMBOLS
.INSRT MID:MACROS
.INSRT WEB:5TABLE

PDL:	-PDLen,,.
	Block PDLen

IdxJFN:	0
SaveBP:	0

.INSRT MID:PRARG

Begin:	RESET
	Move P,PDL
	Movx 1,GJ%SHT
	Hrroi 2,[Asciz "W7:INDEX.BIN"]
	GTJFN
	  .Lose
	Movem 1,IdxJFN
	Move 2,[Field(36.,OF%BSZ)+OF%RD]
	OPENF
	  .Lose
	SIZEF
	  .Lose
	Movss 1			;JFN,,0
	Move 2,[.FHSLF,,IdxPag]
	Txo 3,PM%CNT\PM%RD\PM%PLD
	PMAP
	  .Lose
	Hrlz Z,IdxAdr		;# entries,,0
	Movns Z			;-# entries,,0
	Hlr Z,IdxAdr

Loop:	Hlrz A,IdxAdr+2(Z)	;Index-file-relative address of word
	Add A,[440500,,IdxAdr]	;Absolute BP to word
	Movem A,SaveBP		;Save one for later
	Setz B,			;N characters in word
Loop0:	Ildb 1,A		;Now count the characters.
	Skipe 1
	  Aoja B,Loop0
	Caig B,1		;Don't do the trivial
	  Jrst Next		;  cases.
	Move C,SaveBP		;BP to start
	Addi B,1
	Lsh B,-1		;(N + 1) / 2
Loop1:	Ildb 1,C
	Seto 2,
	Adjbp 2,A
	Ldb 3,2
	Came 1,3
	  Jrst Next
	Move A,2
	Sojg B,Loop1
	Move A,SaveBP
Loop2:	Ildb 1,A
	Jumpe 1,Loop3
	Move 1,5Table(1)
	PBOUT
	Jrst Loop2

Loop3:	Hrroi 1,[Asciz "
"] ?	PSOUT
Next:	Aobjn Z,Loop
Die:	.Logout
	Jrst Die

Variables
Constants

IdxPag==<./777>+1
IdxAdr=IdxPag*1000

	End Begin
