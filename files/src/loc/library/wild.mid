;-*-Midas-*-

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;	This is a user-context replacement for the WILD% JSYS,
;;	using the very same code, more or less.
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

.SCALAR SVMSK,SVPTR,WFLAGS

UWild:	Push P,4
	Push P,5
	Hllzm 1,WFLAGS		;Save flag bits
	Tlz 1,-1		;Flush flag bits
	Caie 1,.WLSTR		;Compare strings?
	  Jrst UWildL
	Hll 1,WFLAGS
	Tlce 2,-1
	  Tlcn 2,-1
	    Hrli 2,440700
	Tlce 3,-1
	  Tlcn 3,-1
	    Hrli 3,440700
	Setzb 5,SVMSK
UWild1:	Ildb 4,2		;Get a wild character
	Jumpe 4,UWEOW1
	Move 5,4
	Cain 5,"*
	  Jrst [Movem 3,SVPTR
		Movem 2,SVMSK
		Jrst UWild1]
Igno5W:	Ildb 4,3
	Jumpe 4,UWildL
	Caie 5,"%
	  Cain 5,(4)
	    Jrst UWild1
	Tlne 1,(WL%LCD)		;Want case-sensitivity?
	  Jrst UWildF		;  Yes, and not a match.
	Cail 4,"a
	  Caile 4,"z
	    Skipa
	   Trz 4,40
	Cail 5,"a
	  Caile 5,"z
	    Skipa
	   Trz 5,40
	Cain 5,(4)		;After making same case, equal?
	  Jrst UWild1		;  Yeah, now they are!
UWildF:	Skipn 2,SVMSK
	  Jrst UWildL
	Ibp SVPTR
	Move 3,SVPTR
	Jrst UWild1

UWEOW1:	Cain 5,"*
	  Jrst UWildW
	Ildb 4,3
	Jumpn 4,UWildF
UWildW:	Tdza 1,1
UWildL:	  Movsi 1,(WL%NOM)
	Pop P,5
	Pop P,4
	Popj P,

$WILD=<Pushj P,UWild>
