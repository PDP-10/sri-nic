; UPD ID= 108, SNARK:<5.1.MONITOR>VERSIO.MAC.5,   6-Jan-83 10:06:39 by COBB
;Make version # 5101 (octal) for SDC tape...
; UPD ID= 81, SNARK:<5.1.MONITOR>VERSIO.MAC.4,  12-Oct-82 10:45:12 by COBB
;Bump VEDIT from 2570 to 2600
; UPD ID= 4, SNARK:<5.1.MONITOR>VERSIO.MAC.3,   6-Jun-82 10:55:16 by GRANT
;Fix edit number now that new 5.1 directory has been created
;<5.MONITOR>P3-VERSIO.MAC.1, 31-Mar-82 14:22:02, EDIT BY GRANT
;Change VMINOR for 5.1
; UPD ID= 348, SNARK:<5.MONITOR>VERSIO.MAC.393,   8-Dec-81 08:49:35 by GRANT
;Fix VEDIT
; -7390 ID= 1310, SNARK:<5.MONITOR>VERSIO.MAC.56,  24-Nov-80 17:16:12 by DONAHUE
; UPD ID= 427, SNARK:<5.MONITOR>VERSIO.MAC.2,  10-Apr-80 10:46:14 by LYONS
;UPDATE FOR V5
;<4.1.MONITOR>VERSIO.MAC.930,  7-Nov-79 15:57:55, EDIT BY R.ACE
;NOW CHANGE TSVN AND VTSVN TO READ "4.1" INSTEAD OF "4A"
;ALSO CHANGE NAME OF SETSPD FROM "4A-SETSPD" TO "4-1-SETSPD"
;<4.1.MONITOR>VERSIO.MAC.925,  5-Nov-79 21:20:30, EDIT BY R.ACE
;CHANGE VERSION NUMBER TO 4.1
;<4.MONITOR>VERSIO.MAC.691, 11-Mar-79 13:20:38, EDIT BY KONEN
;UPDATE COPYRIGHT FOR RELEASE 4
;<4.MONITOR>VERSIO.MAC.310, 21-Jul-78 13:38:35, Edit by HELLIWELL
;ALLOW VWHO TO BE SET EXTERNALLY
;<4.MONITOR>VERSIO.MAC.212, 16-May-78 18:08:25, EDIT BY MILLER
;ADD MINOR VERSION TO SETSPD NAME
;<4.MONITOR>VERSIO.MAC.73, 31-Jan-78 15:10:51, Edit by MCLEAN
;CHANGE VERSION NUMBER TO 4
	TITLE VERSIO - MONITOR VERSION NUMBER AND NAME MODULE
	SEARCH PROLOG

VEDIT==^D2625
VMAJOR==5
VMINOR==1
IFNDEF VWHO,<VWHO==0>
SCVN==<VWHO>B2+<VMAJOR>B11+<VMINOR>B17+VEDIT

;THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY ONLY BE USED
;  OR COPIED IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;COPYRIGHT (C) 1976,1977,1978,1979 BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.

;THE FOLLOWING MACRO GENERATES THE STRING:
;
;DEFINE SETVER(FOO)
;<FOO(WX(Y)-Z)
;>
;
;THE W,X,Y,Z ARE CALCULATED BY THE MACRO LIKE THIS:

;W - THE NUMBERICAL VALUE OF VMAJOR
;
;X - THE LETTER CORRESPONDING TO THE VALUE OF VMINOR.  @=0, A=1...
;
;Y - THE NUMERICAL VALUE OF VEDIT
;
;Z - THE NUMERICAL VALUE OF VWHO

	DEFINE	.CLNAM<
	  DEFINE  .CLNM(MINOR,WHO)<
	    IFE VMINOR,<
	      IFE VWHO,< .NAME(\VMAJOR,,\VEDIT,)>
	      IFN VWHO,< .NAME(\VMAJOR,,\VEDIT,-WHO)>>
	    IFN VMINOR,<
	      IFE VWHO,< .NAME(\VMAJOR,<.>MINOR,\VEDIT,)>
	      IFN VWHO,< .NAME(\VMAJOR,<.>MINOR,\VEDIT,-WHO)>>>
	IFGE VMINOR-^D64,< VMINOR==0
	  PRINTX %MINOR VERSION TOO LARGE - IGNORED>
	IFGE VWHO-^D8,< VWHO== 0
	  PRINTX %VWHO IS TOO LARGE - IGNORED>
	.CLNM(\VMINOR,\VWHO)
>


;NOW DEFINE A .NAME MACRO TO MAKE A MACRO

	DEFINE	.NAME(V,M,E,W)<
DEFINE SETVER(FOO)
<FOO(<V'M'('E')'W>)
>>

;USE  IT!

.CLNAM

;MACRO TO BUILD VERSION TEXT

   IFNDEF SETV1,<
DEFINE SETV1 (VTXT)<
   DEFINE SYSTXT (FOO)<
	FOO <TOPS-20 System, TOPS-20 Monitor VTXT>>>>

	.PSECT NRCOD		;INCLUDE WITH SWAPPABLE CODE


;SYSTEM VERSION TEXT

SETVER (SETV1)			;GET SYSTXT DEFINED
DEFINE SETV2 (VTXT) <
   DEFINE SYSTXT (FOO)<
      FOO <, TOPS-20 Monitor VTXT>>>	;MACRO TO AID IN GENERATION OF VERSION #
				; PORTION OF MONITOR NAME ONLY


DEFINE FOO (A)<
	ASCIZ @A@>

TSVN::	SYSTXT (FOO)
	0			;ROOM FOR PATCHES
TSVNT==:.-TSVN			;LENGTH OF TSVN IN WORDS

SVNM=:SCVN			;DEFINE COMPOSITE VERSION NUMBER

	SETVER (SETV2)		;REDEFINE SYSTXT
VTSVN::	SYSTXT (FOO)		;GENERATE STRING OF JUST THE VERSION
VTSVNT==:.-VTSVN		;NO. OF WORDS IN STRING
;GENERATE ASCIZ NAME OF SETSPD

;EXAMPLES:  VERSION 4:	   SYSTEM:4-SETSPD.EXE
;	    VERSION 4.1:   SYSTEM:4-1-SETSPD.EXE

DEFINE BSETSPD (R,R1)<
	IFE VMINOR,<
	  ASCIZ/SYSTEM:'R'-SETSPD.EXE/>
	IFG VMINOR,<
	  ASCIZ/SYSTEM:'R'-'R1'-SETSPD.EXE/>
	> ;END DEFINE BSETSPD

SETSPD::BSETSPD (\VMAJOR,\VMINOR)

;PUT NUMERICAL FORM OF VERSION NUMBER IN .JBVER (137)

	.ENDPS NRCOD
	LOC 137			;PUT IN STANDARD PLACE
	SCVN

	END
