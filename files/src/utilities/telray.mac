;<3A-UTILITIES>TELRAY.FAI.10, 25-Aug-79 07:41:15, Edit by MRC
;<SOURCES.UTILITIES>TELRAY.FAI.9, 21-Aug-79 20:01:33, Edit by ADMIN.MRC
;-*-FAIL-*-
TITLE TELRAY -- SET UP VARIOUS PARAMETERS FOR THE TELERAY 1061.

SEARCH MONSYM,MACSYM

.END==0
.REQUIRE SYS:MACREL

PDL:	BLOCK 10
JFN:	0

START:	RESET
	MOVEI 1,.PRIOU
	GTTYP
	CAIE 2,.TTTEL		;IS THIS A TELERAY?
	 HALTF			;NO, NO NEED TO GO ON.
	MOVE P,[IOWD 10,PDL]
	MOVSI 1,(GJ%FOU!GJ%SHT)
	HRROI 2,[ASCIZ/TTY:/]
	GTJFN
	 JRST OOPS
	MOVE 2,[8B5+OF%WR]
	OPENF
	 JRST OOPS
	MOVEM 1,JFN

	HRROI 2,[BYTE (7) 33,"B",.END]
	CALL DSOUT		;LOCK KEYBOARD

	HRROI 2,[BYTE (7) 177,177,177,177,177,177,177,177,177,.END]
	CALL DSOUT

	HRROI 2,[BYTE (7) 33,"U","0","1",3,3,33,"V",.END]
	CALL DSOUT		;FN1=^C^C

	HRROI 2,[BYTE (7) 33,"U","0","2",30,23,3,33,"V",.END]
	CALL DSOUT		;FN2=^X^S^C

	HRROI 2,[BYTE (7) 33,"Y","1","(",33,"F",33,"Y","1","0",33,"F",.END]
	CALL DSOUT		;TABS AT 9,17

	HRROI 2,[BYTE (7) 33,"Y","1","8",33,"F",33,"Y","1","@",33,"F",.END]
	CALL DSOUT		;TABS AT 25,33

	HRROI 2,[BYTE (7) 33,"Y","1","H",33,"F",33,"Y","1","P",33,"F",.END]
	CALL DSOUT		;TABS AT 41,49

	HRROI 2,[BYTE (7) 33,"Y","1","X",33,"F",33,"Y","1","`",33,"F",.END]
	CALL DSOUT		;TABS AT 57,65

	HRROI 2,[BYTE (7) 33,"Y","1","h",33,"F",33,"Y","1","o",33,"F",.END]
	CALL DSOUT		;TABS AT 73,80

	HRROI 2,[BYTE (7) 33,"c",.END]
	CALL DSOUT		;KBD UNLOCKED

	HRROI 2,[BYTE (7) 14,.END]
	CALL DSOUT		;SEND A FORM FEED TO KEEP IT NEAT.
	HALTF
	CLOSF
	JRST START

DSOUT:	HRRZ 1,JFN
	MOVEI 3,100		;MAX 100 CHAR.
	MOVEI 4,.END
	SOUT
	 ERJMP OOPS
	RET

OOPS:	HRROI 1,[ASCIZ/OOPS
/]
	PSOUT
	HALTF
	JRST START

END START
