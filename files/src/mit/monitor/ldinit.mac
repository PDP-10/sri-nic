; UPD ID= 419, SNARK:<5.MONITOR>LDINIT.MAC.2,  11-Apr-80 13:50:32 by HALL
;ADD PSECT RSDAT TO PBEGIN INVOCATION
;<4.MONITOR>LDINIT.MAC.10, 21-Jul-79 14:14:06, EDIT BY GILBERT
;REMOVE EXP 0 INSERTED IN LDINIT.MAC.8 -- LINK BUG HAS BEEN FIXED.
;<4.MONITOR>LDINIT.MAC.9,  4-Mar-79 18:10:12, EDIT BY KONEN
;UPDATE COPYRIGHT FOR RELEASE 4
;<4.MONITOR>LDINIT.MAC.8, 15-Jan-79 07:40:31, EDIT BY GILBERT
;More TCO 4.2155:
;	Change SYTBL to SYVAR.
;	Add an EXP 0 to bypass a LINK bug.
;<4.MONITOR>LDINIT.MAC.7,  8-Jan-79 06:49:49, EDIT BY GILBERT
;TCO 4.2155 - Implement hidden symbol tables:
;	Make names of PSECT start labels uniform.
;<4.MONITOR>LDINIT.MAC.6,  3-Jan-79 20:24:15, EDIT BY GILBERT
;SUPPRESS PSECT START PAGE NUMBERS TO DDT TYPEOUT
;<4.MONITOR>LDINIT.MAC.5, 10-Oct-78 09:55:49, EDIT BY MILLER
;SMALL CHANGE TO DEFJS
;<4.MONITOR>LDINIT.MAC.4, 16-Aug-78 02:02:27, Edit by MCLEAN
;<4.MONITOR>LDINIT.MAC.3, 16-Aug-78 01:49:51, Edit by MCLEAN
;FIX IT SO .EXEC ENTRY IS SECTION 1
;<4.MONITOR>LDINIT.MAC.2,  4-Aug-78 23:31:59, Edit by MCLEAN
;MAKE UJSYS0 BE SECTION 1


;THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY ONLY BE USED
;  OR COPIED IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;COPYRIGHT (C) 1976,1977,1978,1979 BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.

	SEARCH PROLOG,MONSYM
	TITLE LDINIT
	ENTRY $LDINIT
	$LDINIT==:0
	SALL

DEFINE PBEGIN(PSECT)<
    IRP PSECT,<
	.PSECT PSECT
PSECT::	BLOCK 0
	PSECT'P==:PSECT_-PGSFT
	.ENDPS PSECT
    >
>

PBEGIN <RSCOD,INCOD,RSDAT,SYVAR,PPVAR,RSVAR,PSVAR>
PBEGIN <JSVAR,NPVAR,NRCOD,BGSTR,BGPTR,NRVAR>


MAXJS==777			;MAXIMUM JSYS NUMBER
;JSYS DISPATCH TABLE.

	.PSECT RSCOD
JSTAB::

;ON PASS1 DEFINE THE SLOTS TAKEN BY REAL JSYS DEFINITIONS

IF1,<
   DEFINE DEFJS (NAM,VAL,SECN,COD,OLDNEW) <
      IFG <VAL-MAXJS>,<PRINTX *** JSYS NAM .GT. 777 ***>
      IFDIF <COD>,<NIM>,<
	IFB <SECN>,<
	  %%'VAL==<PCU>B53,,.'NAM##	;;DEFINE SLOT AND REFERENCE SYMBOL
	>
	IFNB <SECN>,<
	  %%'VAL==SECN##+<PCU>B53,,.'NAM##
	>
      >
   >

;NOW EXPAND JSYS LIST

	JSLIST

> ;;; END OF IF1

;ON PASS2 FILL TABLE WITH DEFINES ENTRIES AND DEFAULTS IF
; SLOT IS EMPTY

IF2,<
   DEFINE MAKENT (SLOT) <
  IFNDEF %%'SLOT,<MSEC1+<PCU>B53,,UJSYS0##>
  IFDEF %%'SLOT,<
	%%'SLOT
	PURGE %%'SLOT
      >
   >

%%CNT==0		;INIT COUNTER
REPEAT MAXJS+1,<			;;REPEAT FROM 0 TO 777
	MAKENT (\%%CNT)	;MAKE AN ENTRY
	%%CNT==%%CNT+1
>

> ;;; END IF2


	.ENDPS RSCOD

	END
