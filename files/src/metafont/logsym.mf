% Some new math symbols

new slant; slant=0; trxy 0;	% the non-script characters are unslanted


"Logical deduction";
cpen;
if fixwidth=0: 	call charbegin('067,9,0,0,ph,0,0); top0y1=h;
   else: call charbegin('067,9,0,0,3.5pu+pa,3.5pu-pa,0); top0y1=a+3.5u;
fi;
bot0y2=0; x1=x2=x3;
lft0x3=round u; x4=r-x3; y3=y4=.5[y1,y2];
w0 draw 1..2;	% stem
draw 3..4.	% bar


"Logical implication";
cpen;
if fixwidth=0:	call charbegin('077,9,0,0,ph,0,0); top0y1=h;
   else: call charbegin('077,9,0,0,3.5pu+pa,3.5pu-pa,0); top0y1=a+3.5u;
fi;
bot0y2=0; x1=x2=x3;
lft0x3=round u; x4=r-x3; y3=y4=a;
.38[y1,y2]=y5; y5=y6;
.38[y2,y1]=y7; y7=y8;
x5=x7=x1; x6=x8=x4;
w0 draw 1..2;	% stem
draw 5..6;	% top bar
draw 7..8.	% bottom bar


"Left Frege Quote";
cpen;
if fixwidth=0: call charbegin('156,8,0,0,ph,0,0); top0y1=h;
    else: call charbegin('156,9,0,0,3.5pu+pa,3.5pu-pa,0); top0y1=a+3.5u;
fi;
bot0y3=round(.5h); x1=x2=x3;
lft0x3=round u; x4=r-x3; y4=y1;
w0 draw 1..3;	% stem
draw 1..4.	% bar


"Right Frege Quote";
cpen;
if fixwidth=0: 	call charbegin('157,8,0,0,ph,0,0); top0y1=h;
    else: call charbegin('157,9,0,0,3.5pu+pa,3.5pu-pa,0); top0y1=a+3.5u;
fi;
bot0y3=round(.5h); x1=x2=x3;
rt0x3=round(r-u); x4=x3-r+3u; y4=y1;
w0 draw 1..3;	% stem
draw 1..4.	% bar


"Belief deduction";
cpen;
if fixwidth=0: 	call charbegin('167,9,0,0,ph,0,0); top0y1=h;
   else: call charbegin('167,9,0,0,3.5pu+pa,3.5pu-pa,0); top0y1=a+3.5u;
fi;
bot0y2=0; x1=x2=x3;
lft0x3=round u; x4=r-x3; y3=y4=.5[y1,y2];
w0 draw 1..2;	% stem
draw 3..4;	% bar
y5=.5[y1,y3]; y6=.5[y2,y3];  % bow halfway points
x5=x1+.3r; x6=x5;
draw 1{6,-1}..5..3{-6,-1};   % upper bow
draw 2{6,1}..6..3{-6,1}.     % lower bow



