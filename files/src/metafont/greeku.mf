% These Greek letters are designed on the same principle as the Roman upper case
% letters on file \.{romanu.mf}. Only letters distinct from roman forms appear here.
% For text spacing, set $\\{mi}=0$; for math spacing, set $\\{mi}=1$.
% Character codes \000--\012 are generated.

new mc,lbowl,rbowl,rstem,rv,hic;	% quantities used to compute spacing
mc=mi/pu;	% converts to relative units when $\\{mi}=1$
lbowl=.3phh.slant+.5pu;	% used at left of upper-case bowl
rbowl=.7phh.slant-.5pu;	% used at right of upper-case bowl
rstem=phh.slant+(ucs+2sc-1.5)pu;	% used at right of tall stem
rv=phh.slant+(ucs+2sc-1)pu;	% used at right of tall diagonal
hic=1-.5mi;	% used when half the italic correction goes into \\{rtcorr}
"Upper case Greek Gamma";
call charbegin('000,11,2sc,sc-mc(armic-2.5pu),phh,0,mi[armic,2.5pu]);
hpen; lft4x1=round 2u; x2=x1; top4y1=hh; bot4y2=0;
w4 draw1..2;	% stem
if ucs0:
	call `a serif(1,4,2,-ucs);
	call `b serif(1,4,2,.5ucs);	% upper stem serif
	call `c serif(2,4,1,-ucs);
	call `d serif(2,4,1,ucs);	% lower stem serif
fi;
rt0x3=round(r-1.5u); x4=good0(x3+.5u); y3=y1; y4=y3-as;
call `e arm(1,3,4,.7u).	% upper arm and serif
"Upper case Greek Delta";
call charbegin('001,15,0,0,phh,0,0);
hpen; lft0x2=round u; rt4x4=round(r-u); bot0y2=0; bot4y4=0;
top4y5=hh+o+oo; y6=y5=y8;
x6-x2=x4-x5; rt4x5=rt0x8; x6=x8-del; x1=x2; x3=x4;
vpen; bot9y1=0; y3=y1;
w9 draw 1..3;	% bar line
rpen#; w9 draw 5..4;	% erase excess at lower right
hpen; w4 draw 5..4;	% right diagonal
lpen#; w4 draw 6..2; w9 draw 6..2;	% erase excess at left
hpen; w0 draw 8..6..6..2;	% left diagonal
y7=y4; rt0x7=rt4x4;
w0 draw 2..7..7..8.	% sharpen lower right corner
"Upper case Greek Theta";
call charbegin('002,14,mc.lbowl,-mc(rbowl-pu),ph,pdd,mi[rbowl,pu]);
hpen;
if fixwidth0: new save; save=sqrttwo;
	new sqrttwo; sqrttwo=sqrt save; % super-superellipse
	lft5x2=round 1.5u;
else: lft5x2=round u;
fi;
x1=r-x1;	% axis of left-right symmetry
top0y1=hh+o;
bot0y2=-o; y3=y2; x3=r-x2;
call `a darc(1,2,w5);	% left part of bowl
call `b darc(1,3,w5);	% right part of bowl
lft0x4=round(rt5x2+u); x5=r-x4; y4=y5=.5[y1,y2];
vpen;
if ucs=0: w9 draw 4..5;	% bar line
else: call bar(4,5,9);	% bar line
	x6=x7=x4; x8=x9=x5; y7=y9=.6hh; y6=y8=.4hh;
	w0 draw 6..7;	% left serif
	draw 8..9;	% right serif
fi;
if fixwidth0: new sqrttwo; sqrttwo=save;
fi.
"Upper case Greek Lambda";
call charbegin('003,11,2sc,2sc,phh,0,0);
hpen; new w99; w99=round .5[w4,w5];
lft0x1=round 1.5u; bot0y1=0;
rt99x4=round(r-1.5u); bot99y4=0;
top0y3=top99y2=hh+o+oo;
x3-x1=x4-x2; rt99x2=rt0x3+del;
w99 draw 2..4; w0 draw 2..3;	% right diagonal stroke
lpen#; w99 draw 3..1;	% erase excess at upper left
hpen; w0 draw 3..1;	% left diagonal stroke
if ucs0:
	call `a dserif(1,0,3,-ucs);
	call `b dserif(1,0,3,+ucs);	% left serifs
	call `c serif(4,99,2,-ucs);
	call `d serif(4,99,2,+ucs);	% right serifs
fi.
"Upper case Greek Xi";
call charbegin('004,11,0,-.5mc(phh.slant-.5pu),phh,0,hic(phh.slant-.5pu));
new w99; 
if ucs=0: w99=w4;
else: w99=round .9[w6,w9];
fi;
vpen; top99y1=hh; y1=y2; y3=y4=good99 .5hh; bot99y5=0; y5=y6;
lft99x1=round u; rt99x2=round(r-u); x5=x1; x6=x2;
lft99x3=round 2u; rt99x4=round(r-2u);
if ucs=0: w99 draw 1..2;	% upper bar
	draw 3..4;	% middle bar
	draw 5..6;	% lower bar
else: call bar(1,2,99);	% upper bar
	call bar(3,4,99);	% middle bar
	call bar(5,6,99);	% lower bar
	new ss; ss=ucs.aspect.u+eps;
	if bot99y1-ss<top99y3+ss: new ss; bot99y1-ss=top99y3+ss+eps;
	fi;
	x7=x10=x1; x8=x9=x3; x11=x14=x2; x12=x13=x4;
	bot6y7=bot99y1-ss; top6y8=top99y3+ss;
	bot6y9=bot99y3-ss; top6y10=top99y5+ss;
	y11=y7; y12=y8; y13=y9; y14=y10;
	w6 draw 7..1; draw 11..2;	% upper serifs
	draw 8..9; draw 12..13;	% middle serifs
	draw 10..5; draw 14..6;	% lower serifs
fi.
"Upper case Greek Pi";
call charbegin('005,13,2sc,2sc-.5mc.rstem,phh,0,hic.rstem);
hpen; lft4x1=round 2u; x2=x1;
rt4x3=round(r-2u); x4=x3;
top4y1=hh; y1=y3;
bot4y2=0; y2=y4;
w4 draw 1..2;	% left stem
w4 draw 3..4;	% right stem
if ucs0: call `a serif(1,4,2,-ucs);
		call `b serif(1,4,2,.5ucs);	% upper left serif
	call `c serif(2,4,1,-ucs);
		call `d serif(2,4,1,ucs);	% lower left serif
	call `e serif(3,4,4,-.5ucs);
		call `f serif(3,4,4,ucs);	% upper right serif
	call `g	serif(4,4,3,-ucs);
		call `h serif(4,4,3,ucs);	% lower right serif
fi;
w0 draw 1..3.	% bar
"Upper case Greek Sigma";
call charbegin('006,13,0,sc-.5mc.armic,phh,0,hic.armic);
new w99; w99=round .25[w6,w9];
hpen; lft0x1=round u; rt0x2=round(r-1.5u); x3=good0(x2+.5u);
top0y1=hh; y2=y1; y3=y2-as;
vpen; bot99y4=0; y5=y4; top6y6=top99y4; y7=y6; y8=y7+as;
x4=x6=x1; x5=x7=x2; x8=x3;
call `a arm(1,2,3,.6u);	% upper arm and serif
call `b arm(6,7,8,.6u);	% lower arm and serif
w99 draw 4..5;
if ucs=0: draw 5..8;	% thicken lower arm
else: if w0w4: draw |w99|5..|w6|8;	% thicken lower arm
	else: draw 5..8;	% thicken lower arm
	fi;
fi;
hpen; x9=x1; x10=.5[x9,x11]; x16=round 5u; lft4x12=lft0x15;
lft4x99=x16; rt4x99=rt0x11; lft4x13=lft0x1; rt4x13=rt0x14;
bot0y9=0; y10=.5[y9,y11]; y11=y16=.5hh; y12=y15; y13=y14=y1;
new aa,bb; lft0x15=aa[lft0x1,x16]; y15=aa[y1,y16];
lft0x15=bb[x9,x11]; y15=bb[y9,y11];
w4 draw 13..12;	% upper diagonal
w0 draw 1..15;	% sharpen upper left corner
rpen#; w4 draw 10..11;	% erase excess at right
lpen#; w99 draw 9..10;	% erase excess at left
hpen; w0 draw 9..11;	% lower diagonal
draw 14..11.	% sharpen middle corner
"Upper case Greek Upsilon";
call charbegin('007,14,mc(.8phh.slant+.5pu),-mc(.8phh.slant-3pu),
				phh,0,mi[.8phh.slant-.5pu,2.5pu]);
hpen; x1=good4 .5r; bot4y1=0; x2=x1; y2=.5hh;
w4 draw 1..2;	% stem
if ucs0: call `a serif(1,4,2,-1.25ucs);
	call `b serif(1,4,2,1.25ucs);	% serif
fi;
new w99; w99=round .5w4;
rt99x3=rt4x2; lft99x4=lft4x2;
lft99x5=x2-.25u; x7=x9=x13=10.5u;
vpen; rt8x11=round(r-u); y11=.8hh;
top8y13=top6y9=hh+o; bot8y13=bot6y7;
y3=y4=y5=y6=y2; y7=y8; y9=y10; y11=y12; y13=y14;
x3+x4=x5+x6=x7+x8=x9+x10=x11+x12=x13+x14;	% left-right symmetry
w9 draw 13{1,0}..11{0,-1};	% right stroke
draw 14{-1,0}..12{0,-1};	% left stroke
hpen; w99 ddraw 5{0,1}..9{1,0}, 3{0,1}..7{1,0};
ddraw 6{0,1}..10{-1,0}, 4{0,1}..8{-1,0}.	% middle strokes
"Upper case Greek Phi";
call charbegin('010,13,mc(.5phh.slant+.5pu),-mc(.5phh.slant-.5pu),
				phh,0,mi[.5phh.slant-.5pu,0]);
hpen; x1=good4 .5r; bot4y1=0; x2=x1; top4y2=hh;
w4 draw 1..2;	% stem
if ucs0: call `a serif(1,4,2,-ucs);
	call `b serif(1,4,2,ucs);	% upper serif
	call `c serif(2,4,1,-ucs);
	call `d serif(2,4,1,ucs);	% lower serif
fi;
x3=x1; y3=.8hh; y4=y5=.2hh;
lft5x4=round u; x3-x4=x5-x3;	% left-right symmetry
call `e darc(3,4,w5); call `f darc(3,5,w5).	% bowl
"Upper case Greek Psi";
call charbegin('011,14,mc(.8phh.slant),-.5mc(.8phh.slant-2.5pu),
				phh,0,hic(.8phh.slant-.5pu)+mi.pu);
hpen; x1=good4 .5r; bot4y1=0; x2=x1; top4y2=hh;
w4 draw 1..2;	% stem
if ucs0: call `a serif(1,4,2,-ucs);
	call `b serif(1,4,2,ucs);	% upper serif
	call `c serif(2,4,1,-ucs);
	call `d serif(2,4,1,ucs);	% lower serif
fi;
x3=x1; y3=.2hh; lft4x6=u; x4=good4 3u; y4=.6hh; y6=.8hh;
y4=y5; y6=y7; x3-x4=x5-x3; x3-x6=x7-x3;	% left-right symmetry
w4 draw 6{1,0}..4{0,-1}..3{1,0};	% left stroke
draw 7{-1,0}..5{0,-1}..3{-1,0}.		% right stroke
"Upper case Greek Omega";
call charbegin('012,13,mc(2/3pe.slant+.5pu),-.5mc(.75phh.slant-.5pu),
				phh,0,hic(.75phh.slant-.5pu));
hpen; top0y1=hh+o; y2=.7hh; rt5x2=rt0x8=round(r-u);
vpen; bot7y8=bot6y6=0; y4=y8; x4=x6=good0 8.5u;
x1+x1=x2+x3=x4+x5=x6+x7=x8+x9=r;	% left-right symmetry
y2=y3; y4=y5; y6=y7; y8=y9;
w7 draw 4..8;	% right bar
draw 5..9;	% left bar
lpen#; .5(x6-x7) draw 2{0,-1}..6{x6-x2,5/7(y6-y2)};	% erase middle
rpen#; .5(x6-x7) draw 3{0,-1}..7{x7-x3,5/7(y7-y3)};	% ditto
call `a arc(1,2,w5); call `b arc(1,3,w5);	% bowl
hpen; draw |w5|2{0,-1}..|w0|6{x6-x2,5/7(y6-y2)};	% right diagonal
draw |w5|3{0,-1}..|w0|7{x7-x3,5/7(y7-y3)};	% left diagonal
if ucs0: x12=x8; bot0y10=0; top0y12=2/3e;
	x10+x11=x12+x13=r; y10=y11; y12=y13;
	if w7=w9: x10=x12;
	else: x10+.5u=x12;
		rpen#; .5u+w0 draw 10..12;	% erase excess at right
		lpen#; .5u+w0 draw 11..13;	% erase excess at left
	fi;
	hpen; w0 draw 10..12; draw 11..13;	% serifs
fi.
 