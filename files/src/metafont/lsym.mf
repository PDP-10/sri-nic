% Some new logical math symbols

new slant; slant=0; trxy 0;	% the non-script characters are unslanted
sample=0;

"Logical deduction";
cpen;
if fixwidth=0: 	call charbegin('140,9,0,0,ph,0,0); top10y1=h;
   else: call charbegin('140,9,0,0,3.5pu+pa,3.5pu-pa,0); top10y1=a+3.5u;
fi;
bot10y2=0; x1=x2=x3;
lft10x3=round u; x4=r-x3; y3=y4=.5[y1,y2];
w10 draw 1..2;	% stem
draw 3..4;	% bar
if sample=1: call box(0); fi.


"Logical implication";
cpen;
if fixwidth=0:	call charbegin('141,9,0,0,ph,0,0); top10y1=h;
   else: call charbegin('141,9,0,0,3.5pu+pa,3.5pu-pa,0); top10y1=a+3.5u;
fi;
bot10y2=0; x1=x2=x3;
lft10x3=round u; x4=r-x3; y3=y4=a;
.38[y1,y2]=y5; y5=y6;
.38[y2,y1]=y7; y7=y8;
x5=x7=x1; x6=x8=x4;
w10 draw 1..2;	% stem
draw 5..6;	% top bar
draw 7..8;	% bottom bar
if sample=1: call box(0); fi.


"Left Frege Quote";
cpen;
if fixwidth=0: call charbegin('156,8,0,0,ph,0,0); top11y1=h;
    else: call charbegin('156,9,0,0,3.5pu+pa,3.5pu-pa,0); top11y1=a+3.5u;
fi;
bot11y3=round(.5h); x1=x2=x3;
lft11x3=round u; x4=r-x3; y4=y1;
w11 draw 1..3;	% stem
draw 1..4;	% bar
if sample=1: call box(0); fi.


"Right Frege Quote";
cpen;
if fixwidth=0: 	call charbegin('157,8,0,0,ph,0,0); top11y1=h;
    else: call charbegin('157,9,0,0,3.5pu+pa,3.5pu-pa,0); top11y1=a+3.5u;
fi;
bot11y3=round(.5h); x1=x2=x3;
rt11x3=round(r-u); x4=x3-r+3u; y4=y1;
w11 draw 1..3;	% stem
draw 1..4;	% bar
if sample=1: call box(0); fi.


"Belief deduction";
cpen;
if fixwidth=0: 	call charbegin('167,9,0,0,ph,0,0); top11y1=h;
   else: call charbegin('167,9,0,0,3.5pu+pa,3.5pu-pa,0); top11y1=a+3.5u;
fi;
bot10y2=0; x1=x2=x3;
lft10x3=round u; x4=r-x3; y3=y4=.5[y1,y2];
w10 draw 1..2;	% stem
draw 3..4;	% bar
y5=.5[y1,y3]; y6=.5[y2,y3];  % bow halfway points
x5=x1+.3r; x6=x5;
draw 1{6,-1}..5..3{-6,-1};   % upper bow
draw 2{6,1}..6..3{-6,1};     % lower bow
if sample=1: call box(0); fi.

"logical AND sign";
call charbegin('136,11,0,0,ph,ph-2pa,.5[px,ph].slant-.5pu);
cpen; lft10x1=round u; x3=r-x3; x5=r-x1;
top10y3=m; bot10y1=0; y5=y1;
w10 draw 1..3..3..5;	% diagonals
if sample=1: call box(0); fi.

"logical OR sign";
call charbegin('137,11,0,0,ph,ph-2pa,.5[px,ph].slant-.5pu);
cpen; lft10x1=round u; x3=r-x3; x5=r-x1;
bot10y3=0; top10y1=m; y5=y1;
w10 draw 1..3..3..5;	% diagonals
if sample=1: call box(0); fi.

"Material implication sign";
call charbegin('077,15,0,0,.5[px,ph]+prt/2,.5[px,ph]+prt/2-2pa,
	.5[px,ph].slant-2pu);
cpen; lft10x2=round 2.5u; x2=x3=r-x1;
y2=good10 m; .5[y2,y3]=y1=good10 a;
x4=x5=.5r; y4=y2; y5=y3;
w10 draw 2..4{1,0}..1{0,-1}..5{-1,0}..3;	% stroke
if sample=1: call box(0); fi.
