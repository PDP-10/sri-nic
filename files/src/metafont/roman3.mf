% The Computer Modern Roman family of fonts (by D. E. Knuth, 1979--1981)
danger=mi=dc=0;
input romanu;	% upper case (majuscules)
input romanl;	% lower case (minuscules)
input romand;	% numerals, ampersand, and question mark
input romitp2;	% punctuation symbols common to all roman and italic
input romita2;	% accents common to roman and italic text
input romsp2;   % special characters selected from symbol.mf
codingscheme "TEX TYPEWRITER TEXT";
	input romits3;	% substitutes for ligatures
new mc,lbowl,lhook,rbowl,rhook,rstem,hic;	% spacing corrections
mc=1/pu;
rhook=1/3px.slant+.5pw+.5pu;
lbowl=.3px.slant-.5pwii+pu;
rbowl=.7px.slant+.5pwii-pu;
lhook=2/3px.slant-.5pw-.5pu;
rstem=px.slant+.5pwi-pu;
hic=1-.5mi;
"Lower case Greek lambda";
call charbegin('026,10.5,0,0,ph,0,0);
cpen; top6y1=h; x1=-2u; x2=0; y2=.7[m,h]; x3=r-2u; y3=.125e;
new aa; x4=aa[x2,x3]; y4=aa[y2,y3]; y4=.5[e,m]; x5=1.5u; bot1y5=-oo;
hpen; x6=r-1.5u; bot1y6=-oo;
draw |w0#|1{1,0}..|w1#|2{x3-x2,y3-y2}..3{x3-x2,y3-y2}..
	6{2(x6-x3),y6-y3};	% long diagonal
call `a cdraw(5,4,1,0).	% short diagonal
if fixwidth=0: new k,kk,kkk;	% three degrees of kerning
	k=-.5pu; kk=-1.5pu; kkk=-2pu;
	lig `k: `v: `a kern -1pu, `w: `y: `a kern k,
		`o kern k, `e kern k, `c kern k;
	lig `F: `V: `W:
		`A kern kkk, `o kern kk, `e kern kk,
		`a kern kk, `u kern kk, `r kern kk,
	    `K: `X:
		`O kern k, `C kern k, `G kern k, `Q kern k;
	if ucs0: lig `T: `y kern k, ;
	else: lig `T: `y kern kk, ;
	fi;
	lig `Y: `u kern kk, `r kern kk,
	    `P:	`o kern kk, `e kern kk, `a kern kk, `A kern kk;
	lig `O: `D: `A kern k, `W kern k, `Y kern k, `V kern k, `X kern k;
	if lcs0: lig `h: `m: `n:
		`t kern k, `u kern k, `b kern k,
		`y kern k, `w kern k, `v kern k;
	fi;
	lig `o: `o kern -k, `x kern k,
	    `a:	`v kern k, ;
	if ligs0: lig '044: ;
	fi;
	lig `t: `y kern k,
	    `u: `w kern k;
	if ucs0: lig `R: ;
	fi;
	lig `A: `t kern k,
	    `L: `T kern kk, `O kern k, `U kern k, `C kern k,
		`W kern kkk, `Y kern kk, `G kern k, `V kern kkk,
		`Q kern k;
	texinfo slant, (6+2ls)pu, 3pu, 2pu, px, (18+4ls)pu, 2pu;
else: texinfo slant, 9pu, 0, 0, px, 9pu, 9pu;
fi.
