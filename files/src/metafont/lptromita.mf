% This file contains accents common to text roman and italic fonts.
% It also contains the `\AE', `\OE', and `\%', since these symbols are common
% to the same fonts that the accents are common to.
% Character codes \015--\032, \036, \037, \045 are represented.
% (Actually the accents in positions \025, \026, \031, \032 are
% not generated unless $\\{ligs}0$, since other symbols are substituted
% for those accents in non-ligature fonts.)
"Grave accent";
call charbegin('015,9,0,0,ph,0,2/3[ph,px].slant+.5pw-pwi/6-19/6pu);
cpen; lft1x1=round 2u; x2=2/3[x1,r-x1];
top1y1=h; y2=2/3[h,m];
call `a cdraw(1,2,1,0).	% diagonal
"Acute accent";
call charbegin('016,9,0,0,ph,0,ph.slant-1.5pu);
cpen; rt1x1=round(r-2u); x2=2/3[x1,r-x1];
top1y1=h; y2=2/3[h,m];
call `a cdraw(1,2,1,0).	% diagonal
"Circumflex (hat) accent";
call charbegin('136,9,0,0,ph,0,.5[px,ph].slant+.5pw-2pu);
x1=good0 2.25u; x2=x4=r-x2; x3=r-x1; % left-right symmetry
new w99; w99=round .5[w6,w8];
vpen; y1=y3=.5[m,y0]; top99y0=top6y2=h; bot99y0=bot6y4;
w6 ddraw 2..1, 4..1;	% left point
ddraw 2..3, 4..3.	% right point
"Hachek (hooklet) accent";
call charbegin('020,9,0,0,.75[px,ph],0,.75[px,ph].slant+.5pw-2pu);
x1=good0 2.25u; x2=x4=r-x2; x3=r-x1;	% left-right symmetry;
new w99; w99=round .5[w6,w8];
vpen; top6y1=round .75[m,h]; y1=y3;
top99y20=h; y21=.5[m,y20]; y1-y0=y20-y21;	% inverted circumflex
top99y0=top6y4; bot99y0=bot0y2;
w6 ddraw 2..1, 4..1;	% left point
ddraw 2..3, 4..3.	% right point
"Breve accent";
call charbegin('021,9,0,0,ph,0,ph.slant+.5pw-1.5pu);
x0=x1=good0 2u; x2=r-x2; x3=x4=r-x1;	% left-right symmetry
new w99; w99=round 1/3[w6,w8];
y0=y4=1.25[m,h]; y1=y3; y2=1/3[m,h];
vpen; top99y1=h;
draw |w99#|2{-1,0}..|w6#|1(..0);	% left point
draw |w99#|2{1,0}..|w6#|3(..4).		% right point
"Macron (bar) accent";
call charbegin('022,9,0,0,.4[px,ph]+prt.aspect,0,.4[px,ph].slant+.5pw-.75pu);
x1=good0 1.25u; x2=r-x1; y1=y2;
vpen; new w99; w99=round 1/3[w6,w8]; bot99y1=.4[m,h];
w99 draw 1..2.	% bar
"Umlaut (double dot) accent";
call charbegin('023,9,0,0,ph,0,ph.slant+.5pwiii-2pu);
x1=good3 2.5u; x2=r-x1; y1=y2;
cpen; top3y1=h;
w3 draw 1;	% left dot
draw 2.		% right dot
"Tilde (squiggle) accent";
call charbegin('176,9,0,0,ph,0,ph.slant+.5pwi-1.25pu);
x1=1.75u; x4=r-1.75u; x2=.2[x1,x4]; x3=.8[x1,x4];
new aa,bb,rr,costh,sinth,w99;
aa=1/4(x4-x1); bb=.2(h-m); rr=sqrt(aa.aa+bb.bb);
costh=aa/rr; sinth=bb/rr; w99=.5[w0,w1];
spen(4(costh.costh/w99/w99+sinth.sinth/w0/w0),
	8costh.sinth(1/w99/w99-1/w0/w0),
	4(costh.costh/w0/w0+sinth.sinth/w99/w99),
	0,0,0,0);	% oblique pen in direction $\{(x4-x1)/4,y2-y1\}$
top0y1=.8[m,h]; top0y2=h; y3=y1; y4=y2;
w0 draw 1{x2-x1,2(y2-y1)}..2{1,0}..3{1,0}..4{x4-x3,2(y4-y3)}.	% points and stroke
if ligs0: "Arrow (vector) accent";
	call charbegin('025,9,0,0,.8[px,ph]+prt.aspect/2,0,.5[px,ph]slant);
	cpen; lft10x1=round .5u; x2=r-x1; y1=y2=.7[m,h];
	w10 draw 1..2;	% bar
	rt10x3=round(x2-u); x4=x3;
	y3=y2+.3(h-m); y4=y2-.3(h-m);
	draw 3{.5(x2-x3),y2-y3}..2{x2-x3,.5(y2-y3)};	% upper point
	draw 4{.5(x2-x4),y2-y4}..2{x2-x4,.5(y2-y4)};	% lower point
fi.
if ligs0: "Long Hungarian umlaut accent";
	call charbegin('026,9,0,0,ph,0,ph.slant-pu);
	cpen; x1=good0 2.5u; rt1x2=round .5r;
	x3-x1=x4-x2; rt1x4=round(r-1.5u);
	y1=y3=1/3[m,h]; top1y2=h; y2=y4;
	call `a cdraw(2,1,1,0);	% left diagonal
	call `b cdraw(4,3,1,0);	% right diagonal
fi.
"Scandinavian circle accent";
% This character is designed to be used as an ordinary accent on an `a'
% or to be raised ${2\over3}(\\{ph}-\\{px})$ points and superimposed on an `A'.
call charbegin('027,13,0,0,ph,0,0);
x1=good0 5u; x2=x4=r-x2; x3=r-x1;
hpen; top0y2=h+o+oo; top0y4=round 1/3[m,h]+o+oo; y1=y3=.5[y2,y4];
w0 draw 4{-1,0}..1{0,1}..2{1,0};	% left half of bowl
draw 4{1,0}..3{0,1}..2{-1,0}.	% right half of bowl
"Cedilla accent";
% This character is designed to be used as an ordinary accent on a `c'
% or to be superimposed on a `C'.
call charbegin('030,14,0,0,0,.875pd,0);
x1=x2=7.5u; x3=x2+1.5u; x4=x2-1.5u;
new w99; w99=round .5[w0,w1];
hpen; bot1y1=-o; bot1y2=round(-.25d-o);
w1 draw 1..2;	% stem
bot1y4=round(-.875d-o); y3=.5[y2,y4];
draw 2{1,0}..3{0,-1}..4{-1,0}.	% hook
if ligs0: "Cross for Polish l and L";
% This character is designed to be used as an ordinary accent on an `l'
% or to be raised $\\{ph}-\\{px}$ points and superimposed on an `L'.
	call charbegin('031,4+pwiv/pu,2sc,2sc,px-(ph-px),0,0);
	hpen; lft4 .5[x1,x2]=round 2u; x2-x1=4u-eps;
	top0y2+(h-m)=.8[e,m]; y1+(h-m)=.2[e,m];
	w0 draw 1..2;	% diagonal
fi.
if ligs0: "Tie accent";
% This character is tuned to work best with a dotless roman i.
	call charbegin('032,5,sc,sc,ph,0,px.slant+.5pwi+(sc-2)pu);
	x1=good0 .5r; y1=y3=4/9[m,h]; x2=.5[x1,x3]; x3=good0(r+4u);
	vpen; top10y2=round 8/9[m,h];
	w10 draw 1{0,1}..2{1,0};	% left point
	draw 3{0,1}..2{-1,0};		% right point
fi.
