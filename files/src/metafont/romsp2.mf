% The Computer Modern Symbols family of fonts (by D. E. Knuth, 1979).

if fixwidth=0:	texinfo slant, (6+2ls)pu, 3pu, 2pu, px, (18+4ls)pu;
else: texinfo slant, 9pu, 0, 0, px, 9pu;
fi;
% (The calling file should give the rest of the \&{texinfo}.)
new slant; slant=0; trxy 0;	% the non-script characters are unslanted

"Asterisk at the axis";
call charbegin('052,9,0,0,.5px+pa,.5px-pa,0);
new w99; w99=round(.6pixels.pw+blacker);	% thinner than hairline
new w98; w98=round .7[w99,w1];
cpen; top1y1=round(a+.5m); top1y1-bot1y2=m;
y0=.5[y1,y2]; x0=r-x0; x1=x2=x0;
x3=r-x4; x5=r-x6; x3=x5;	% left-right symmetry
y3=y4; y5=y6; y3-y5=y1-y0; .5[y3,y5]=y0;
x4-x0=(.5sqrt3)lft1 3.75u;	% asterisk will have 60-degree angles if $m=7.5u$
call `a cdraw(1,0,98,99);	% upper arm
call `b cdraw(2,0,98,99);	% lower arm
call `c cdraw(3,0,98,99);	% upper left arm
call `d cdraw(4,0,98,99);	% upper right arm
call `e cdraw(5,0,98,99);	% lower left arm
call `f cdraw(6,0,98,99).	% lower right arm

"Perpendicular sign or lattice bottom";
cpen;
if fixwidth=0: if pa+8pu>ph:
		call charbegin('001,18,0,0,ph,ph-2pa,0); top10y1=h;
	else: call charbegin('001,18,0,0,8pu+pa,8pu-pa,0); top10y1=a+8u;
	fi;
else: call charbegin('001,9,0,0,3.5pu+pa,3.5pu-pa,0); top10y1=a+3.5u;
fi;
.5[y1,y2]=a; x1=x2=.5r;
lft10x3=round u; x4=r-x3; y3=y4=y2;
w10 draw 1..2;	% stem
draw 3..4.	% bar
"Reflexive subset sign";
call charbegin('030,18,0,0,ph,ph-2pa,0);
cpen; lft10x1=round 2.5u; x2=x3=r-x1;
top10y2=h; .5[y2,y3]=y1; y2-y1=(good10 .5[m,h])-(good10 a);
x4=x5=.5r; y4=y2; y5=y3;
w10 draw 2..4{-1,0}..1{0,-1}..5{1,0}..3;	% stroke
x6=x1; x7=x2; y6=y7; bot10y6=2a-h;
draw 6..7.	% bar
"Reflexive superset sign";
call charbegin('031,18,0,0,ph,ph-2pa,0);
cpen; lft10x2=round 2.5u; x2=x3=r-x1;
top10y2=h; .5[y2,y3]=y1; y2-y1=(good10 .5[m,h])-(good10 a);
x4=x5=.5r; y4=y2; y5=y3;
w10 draw 2..4{1,0}..1{0,-1}..5{-1,0}..3;	% stroke
x6=x1; x7=x2; y6=y7; bot10y6=2a-h;
draw 6..7.	% bar

% Special CSL Characters by L. Lamport 10 March 1982
"Box";
call charbegin('017,18,0,0,ph,0,0);
cpen;
new bbside;
bbside=.9h;
x0=.5r; y0=.5h; 
top10y1=top10y2=y0+.5 bbside;
bot10y4=bot10y3=y0-.5bbside;
lft10x1=lft10x4=x0-.5bbside;
rt10x2=rt10x3=x0+.5bbside;
w10 draw 1..2; draw 2..3; draw 3..4; draw 4..1.

"Diamond";
call charbegin('016,18,0,0,ph,0,0);
cpen;
dmdht= h;
x0=.5r; y0=.5h; 
x1=x3=x0;
top10y1=round(y0+.5dmdht);
bot10y3=round(y0-.5dmdht);
y2=y4=y0;
lft10x4=round(x0-.5dmdht);
rt10x2=round(x0+.5dmdht);
w10 draw 1..2; draw 2..3; draw 3..4; draw 4..1.

"Element sign";
call charbegin('007,12,0,0,.5[px,ph]+prt/2,.5[px,ph]+prt/2-2pa,
	.5[px,ph].slant-.5pu);
cpen; rt10x1=round(r-u); lft10x3=round u;
x5=x6=x1; x2=x4=.5(r+2u);
y1=y2=good10(.5[m,h]); y3=y6=a; y4=y5; .5[y5,y1]=y6;
w10 draw 1..2{-1,0}..3{0,-1}..4{1,0}..5;	% bowl
draw 3..6;	% bar
x7=x3;y7=y5; draw 1..7. % slash


"The empty set symbol";
call charbegin('002,9,0,0,phhh,0,phhh.slant-.5pu);
if fixwidth=0: new save; save=sqrttwo; new sqrttwo;
	sqrttwo=sqrt(1.23114413save);	% the constant is $2^{3/10}$
fi;
new w99; w99=round .51[w2,w5];
hpen;
if w99>1.5u: lft99x2=round .5u;
else: x2=good99 1.25u;
fi;
x1=r-x1;	% axis of left-right symmetry
x3=r-x2; top0y1=hhh+o; bot0y2=-o; y3=y2;
call `a darc(1,2,w99); call `b darc(1,3,w99);	% bowl
cpen; x4=x3;y4=y1; draw 2..4; % slash
if fixwidth=0: new sqrttwo; sqrttwo=save;
fi.
