% The Computer Modern Symbols family of fonts (by D. E. Knuth, 1979).
danger=0;
codingscheme "TEX MATHSY";

if fixwidth=0:	texinfo slant, (6+2ls)pu, 3pu, 2pu, px, (18+4ls)pu;
else: texinfo slant, 9pu, 0, 0, px, 9pu;
fi;
% (The calling file should give the rest of the \&{texinfo}.)

% Some new logical math symbols

new slant; slant=0; trxy 0;	% the non-script characters are unslanted
sample=0;

"Logical deduction";
cpen;
if fixwidth=0: 	call charbegin('000,11,0,0,ph,0,0); top10y1=h;
   else: call charbegin('140,9,0,0,3.5pu+pa,3.5pu-pa,0); top10y1=a+3.5u;
fi;
.5[y1,y2]=a; x1=x2=x3;
lft10x3=round u; x4=r-x3; y3=y4=a;
w10 draw 1..2;	% stem
draw 3..4.	% bar
if sample=1: call box(0); fi.


"Logical implication";
cpen;
if fixwidth=0:	call charbegin('001,11,0,0,ph,0,0); top10y1=h;
   else: call charbegin('141,9,0,0,3.5pu+pa,3.5pu-pa,0); top10y1=a+3.5u;
fi;
.5[y1,y2]=a; x1=x2=x3;
lft10x3=round u; x4=r-x3; y3=y4=a;
.38[y1,y2]=y5; y5=y6;
.38[y2,y1]=y7; y7=y8;
x5=x7=x1; x6=x8=x4;
w10 draw 1..2;	% stem
draw 5..6;	% top bar
draw 7..8;	% bottom bar
if sample=1: call box(0); fi.


"Left Frege Quote";
cpen;
if fixwidth=0: call charbegin('002,8,0,0,ph,0,0); top11y1=h;
    else: call charbegin('156,9,0,0,3.5pu+pa,3.5pu-pa,0); top11y1=a+3.5u;
fi;
bot11y3=round(.5h); x1=x2=x3;
lft11x3=round u; x4=r-x3; y4=y1;
w11 draw 1..3;	% stem
draw 1..4;	% bar
if sample=1: call box(0); fi.


"Right Frege Quote";
cpen;
if fixwidth=0: 	call charbegin('003,8,0,0,ph,0,0); top11y1=h;
    else: call charbegin('157,9,0,0,3.5pu+pa,3.5pu-pa,0); top11y1=a+3.5u;
fi;
bot11y3=round(.5h); x1=x2=x3;
rt11x3=round(r-u); x4=x3-r+3u; y4=y1;
w11 draw 1..3;	% stem
draw 1..4;	% bar
if sample=1: call box(0); fi.


"Belief deduction";
cpen;
if fixwidth=0: 	call charbegin('004,11,0,0,ph,0,0); top11y1=h;
   else: call charbegin('167,9,0,0,3.5pu+pa,3.5pu-pa,0); top11y1=a+3.5u;
fi;
.5[y1,y2]=a; x1=x2=x3;
lft10x3=round u; x4=r-x3; y3=y4=a;
% bot10y2=0; x1=x2=x3;
% lft10x3=round u; x4=r-x3; y3=y4=.5[y1,y2];
w10 draw 1..2;	% stem
draw 3..4;	% bar
y5=.5[y1,y3]; y6=.5[y2,y3];  % bow halfway points
x5=x1+.4r; x6=x5;
draw 1{6,-1}..5..3{-6,-1};   % upper bow
draw 2{6,1}..6..3{-6,1};     % lower bow
if sample=1: call box(0); fi.

"logical AND sign";
call charbegin('005,11,0,0,ph,ph-2pa,.5[px,ph].slant-.5pu);
cpen; lft10x1=round u; x3=r-x3; x5=r-x1;
top10y3=m; bot10y1=0; y5=y1;
w10 draw 1..3..3..5;	% diagonals
if sample=1: call box(0); fi.

"logical OR sign";
call charbegin('006,11,0,0,ph,ph-2pa,.5[px,ph].slant-.5pu);
cpen; lft10x1=round u; x3=r-x3; x5=r-x1;
bot10y3=0; top10y1=m; y5=y1;
w10 draw 1..3..3..5;	% diagonals
if sample=1: call box(0); fi.

"Material implication sign";
call charbegin('007,15,0,0,.5[px,ph]+prt/2,.5[px,ph]+prt/2-2pa,
	.5[px,ph].slant-2pu);
cpen; lft10x2=round 2.5u; x2=x3=r-x1;
y2=good10 m; .5[y2,y3]=y1=good10 a;
x4=x5=.5r; y4=y2; y5=y3;
w10 draw 2..4{1,0}..1{0,-1}..5{-1,0}..3;	% stroke
if sample=1: call box(0); fi.

"Box";
call charbegin('010,18,0,0,ph,0,0);
cpen;
new bbside;
bbside=.9h;
x0=.5r; y0=.5h; 
top10y1=top10y2=y0+.5 bbside;
bot10y4=bot10y3=y0-.5bbside;
lft10x1=lft10x4=x0-.5bbside;
rt10x2=rt10x3=x0+.5bbside;
w10 draw 1..2; draw 2..3; draw 3..4; draw 4..1;
if sample=1: call box(0); fi.

"Diamond";
call charbegin('011,18,0,0,ph,0,0);
cpen;
dmdht= h;
x0=.5r; y0=.5h; 
x1=x3=x0;
top10y1=round(y0+.5dmdht);
bot10y3=round(y0-.5dmdht);
y2=y4=y0;
lft10x4=round(x0-.5dmdht);
rt10x2=round(x0+.5dmdht);
w10 draw 1..2; draw 2..3; draw 3..4; draw 4..1;
if sample=1: call box(0); fi.
