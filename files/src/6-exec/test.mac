search monsym
t1=1
t2=2
t3=3
t4=4

start:	RESET
	movei t1,.sysve		;Want version text since .monvr doesn't work
	movei t2,5		;Want the fifth word
	hrl t1,t2
	GETAB			;Read monitor table
	 erjmp error
	move t2,[point 7,t1]	;Let's look at the word
	ildb t3,t2		;Get a byte
	hrroi t1,[asciz/Version 5/]
	caie t3,"5"		;Version 5 monitor?
	hrroi t1,[asciz/Version 6/]
	PSOUT
	HALTF
	jrst .-1

;Prints out the appropriate error message and halts the job.
ERROR:	hrroi t1,[asciz/Error: /]
	ESOUT				;Synchronize things
	movei t1,.priou			;Output to terminal
	hrloi t2,.fhslf			;This fork. most recent error
	movei t3,0			;No limit to byte count
	ERSTR				;Print the error
	 jfcl				;Don't care about more errors
	 jfcl
	hrroi t1,crlf
	PSOUT
	HALTF				;Snuff it
	jrst .-1			;Stay snuffed

crlf:	byte(7)15,12

end	start
